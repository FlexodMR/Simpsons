    <HTML> 
	<HEAD> 
	    <TITLE>perlipc - Perl interprocess communication (signals, fifos, pipes, safe subprocesses, sockets, and semaphores)

</TITLE> 
	</HEAD>

	<BODY>

<!-- INDEX BEGIN -->

<UL>

	<LI><A HREF="#NAME">NAME</A>
	<LI><A HREF="#DESCRIPTION">DESCRIPTION</A>
	<LI><A HREF="#Signals">Signals</A>
	<LI><A HREF="#Named_Pipes">Named Pipes</A>
	<LI><A HREF="#Using_open_for_IPC">Using open() for IPC</A>
	<UL>

		<LI><A HREF="#Filehandles">Filehandles</A>
		<LI><A HREF="#Background_Processes">Background Processes</A>
		<LI><A HREF="#Complete_Dissociation_of_Child_f">Complete Dissociation of Child from Parent</A>
		<LI><A HREF="#Safe_Pipe_Opens">Safe Pipe Opens</A>
		<LI><A HREF="#Bidirectional_Communication_with">Bidirectional Communication with Another Process</A>
	</UL>

	<LI><A HREF="#Sockets_Client_Server_Communica">Sockets: Client/Server Communication</A>
	<UL>

		<LI><A HREF="#Internet_TCP_Clients_and_Servers">Internet TCP Clients and Servers</A>
		<LI><A HREF="#Unix_Domain_TCP_Clients_and_Serv">Unix-Domain TCP Clients and Servers</A>
	</UL>

	<LI><A HREF="#TCP_Clients_with_IO_Socket">TCP Clients with IO::Socket</A>
	<UL>

		<LI><A HREF="#A_Simple_Client">A Simple Client</A>
		<LI><A HREF="#A_Webget_Client">A Webget Client</A>
		<LI><A HREF="#Interactive_Client_with_IO_Sock">Interactive Client with IO::Socket</A>
	</UL>

	<LI><A HREF="#TCP_Servers_with_IO_Socket">TCP Servers with IO::Socket</A>
	<LI><A HREF="#UDP_Message_Passing">UDP: Message Passing</A>
	<LI><A HREF="#SysV_IPC">SysV IPC</A>
	<LI><A HREF="#NOTES">NOTES</A>
	<LI><A HREF="#BUGS">BUGS</A>
	<LI><A HREF="#AUTHOR">AUTHOR</A>
	<LI><A HREF="#SEE_ALSO">SEE ALSO</A>
</UL>
<!-- INDEX END -->

<HR>
<P>
<H1><A NAME="NAME">NAME

</A></H1>
perlipc - Perl interprocess communication (signals, fifos, pipes, safe
subprocesses, sockets, and semaphores)


<P>

<P>
<HR>
<H1><A NAME="DESCRIPTION">DESCRIPTION

</A></H1>
The basic 
<FONT SIZE=-1>IPC</FONT> facilities of Perl are built out of the good old Unix signals, named pipes, pipe opens, the Berkeley socket routines, and SysV 
<FONT SIZE=-1>IPC</FONT> calls. Each is used in slightly different situations.



<P>

<P>
<HR>
<H1><A NAME="Signals">Signals

</A></H1>
Perl uses a simple signal handling model: the <A HREF="#item__SIG">%SIG</A> hash contains names or references of user-installed signal handlers. These handlers will be called with an argument which is the name of the signal that triggered it. 
<FONT SIZE=-1>A</FONT> signal may be generated intentionally from a particular keyboard sequence like control-C or control-Z, sent to you from another process, or triggered automatically by the kernel when special events transpire, like a child process exiting, your process running out of stack space, or hitting file size limit.



<P>

For example, to trap an interrupt signal, set up a handler like this. Do as little as you possibly can in your handler; notice how all we do is set a global variable and then raise an exception. That's because on most systems, libraries are not re-entrant; particularly, memory allocation and 
<FONT SIZE=-1>I/O</FONT> routines are not. That means that doing nearly
 <EM>anything</EM> in your handler could in theory trigger a memory fault and subsequent core
dump.


<P>

<PRE>    sub catch_zap {
        my $signame = shift;
        $shucks++;
        die &quot;Somebody sent me a SIG$signame&quot;;
    }
    $SIG{INT} = 'catch_zap';  # could fail in modules
    $SIG{INT} = \&amp;catch_zap;  # best strategy
</PRE>

<P>

The names of the signals are the ones listed out by <CODE>kill -l</CODE> on your system, or you can retrieve them from the Config module. Set up an
<CODE>@signame</CODE> list indexed by number to get the name and a
<CODE>%signo</CODE> table indexed by name to get the number:


<P>

<PRE>    use Config;
    defined $Config{sig_name} || die &quot;No sigs?&quot;;
    foreach $name (split(' ', $Config{sig_name})) {
        $signo{$name} = $i;
        $signame[$i] = $name;
        $i++;
    }
</PRE>

<P>

So to check whether signal 17 and 
<FONT SIZE=-1>SIGALRM</FONT> were the same, do just this:


<P>

<PRE>    print &quot;signal #17 = $signame[17]\n&quot;;
    if ($signo{ALRM}) {
        print &quot;SIGALRM is $signo{ALRM}\n&quot;;
    }
</PRE>

<P>

You may also choose to assign the strings <CODE>'IGNORE'</CODE> or <CODE>'DEFAULT'</CODE> as the handler, in which case Perl will try to discard the signal or do the default thing. Some signals can be neither trapped nor ignored, such as the 
<FONT SIZE=-1>KILL</FONT> and 
<FONT SIZE=-1>STOP</FONT> (but not the 
<FONT SIZE=-1>TSTP)</FONT> signals. One strategy for temporarily ignoring signals is to use a <CODE>local()</CODE> statement, which will be automatically restored once your block is exited. (Remember that <CODE>local()</CODE> values are ``inherited'' by functions called from within that block.)



<P>

<PRE>    sub precious {
        local $SIG{INT} = 'IGNORE';
        &amp;more_functions;
    }
    sub more_functions {
        # interrupts still ignored, for now...
    }
</PRE>

<P>

Sending a signal to a negative process 
<FONT SIZE=-1>ID</FONT> means that you send the signal to the entire Unix
process-group. This code send a hang-up signal to all processes in the
current process group <EM>except for</EM> the current process itself:


<P>

<PRE>    {
        local $SIG{HUP} = 'IGNORE';
        kill HUP =&gt; -$$;
        # snazzy writing of: kill('HUP', -$$)
    }
</PRE>

<P>

Another interesting signal to send is signal number zero. This doesn't actually affect another process, but instead checks whether it's alive or has changed its 
<FONT SIZE=-1>UID.</FONT>



<P>

<PRE>    unless (kill 0 =&gt; $kid_pid) {
        warn &quot;something wicked happened to $kid_pid&quot;;
    }
</PRE>

<P>

You might also want to employ anonymous functions for simple signal
handlers:


<P>

<PRE>    $SIG{INT} = sub { die &quot;\nOutta here!\n&quot; };
</PRE>

<P>

But that will be problematic for the more complicated handlers that need to reinstall themselves. Because Perl's signal mechanism is currently based on the <CODE>signal(3)</CODE> function from the 
<FONT SIZE=-1>C</FONT> library, you may sometimes be so misfortunate as to run on systems where that function is ``broken'', that is, it behaves in the old unreliable SysV way rather than the newer, more reasonable 
<FONT SIZE=-1>BSD</FONT> and 
<FONT SIZE=-1>POSIX</FONT> fashion. So you'll see defensive people writing signal handlers like this:



<P>

<PRE>    sub REAPER {
        $waitedpid = wait;
        # loathe sysV: it makes us not only reinstate
        # the handler, but place it after the wait
        $SIG{CHLD} = \&amp;REAPER;
    }
    $SIG{CHLD} = \&amp;REAPER;
    # now do something that forks...
</PRE>

<P>

or even the more elaborate:


<P>

<PRE>    use POSIX &quot;:sys_wait_h&quot;;
    sub REAPER {
        my $child;
        while ($child = waitpid(-1,WNOHANG)) {
            $Kid_Status{$child} = $?;
        }
        $SIG{CHLD} = \&amp;REAPER;  # still loathe sysV
    }
    $SIG{CHLD} = \&amp;REAPER;
    # do something that forks...
</PRE>

<P>

Signal handling is also used for timeouts in Unix, While safely protected
within an <A HREF="#item_eval_">eval{}</A> block, you set a signal handler to trap alarm signals and then schedule to
have one delivered to you in some number of seconds. Then try your blocking
operation, clearing the alarm when it's done but not before you've exited
your <A HREF="#item_eval_">eval{}</A> block. If it goes off, you'll use <CODE>die()</CODE> to jump out of the
block, much as you might using <CODE>longjmp()</CODE> or
<CODE>throw()</CODE> in other languages.


<P>

Here's an example:


<P>

<PRE>    eval {
        local $SIG{ALRM} = sub { die &quot;alarm clock restart&quot; };
        alarm 10;
        flock(FH, 2);   # blocking write lock
        alarm 0;
    };
    if ($@ and $@ !~ /alarm clock restart/) { die }
</PRE>

<P>

For more complex signal handling, you might see the standard 
<FONT SIZE=-1>POSIX</FONT> module. Lamentably, this is almost entirely
undocumented, but the <EM>t/lib/posix.t</EM> file from the Perl source distribution has some examples in it.


<P>

<P>
<HR>
<H1><A NAME="Named_Pipes">Named Pipes

</A></H1>

<FONT SIZE=-1>A</FONT> named pipe (often referred to as a 
<FONT SIZE=-1>FIFO)</FONT> is an old Unix 
<FONT SIZE=-1>IPC</FONT> mechanism for processes communicating on the same machine. It works just like a regular, connected anonymous pipes, except that the processes rendezvous using a filename and don't have to be related.



<P>

To create a named pipe, use the Unix command <CODE>mknod(1)</CODE> or on
some systems, <CODE>mkfifo(1).</CODE> These may not be in your normal path.


<P>

<PRE>    # system return val is backwards, so &amp;&amp; not ||
    #
    $ENV{PATH} .= &quot;:/etc:/usr/etc&quot;;
    if  (      system('mknod',  $path, 'p')
            &amp;&amp; system('mkfifo', $path) )
    {
        die &quot;mk{nod,fifo} $path failed;
    }
</PRE>

<P>


<FONT SIZE=-1>A</FONT> fifo is convenient when you want to connect a
process to an unrelated one. When you open a fifo, the program will block
until there's something on the other end.


<P>

For example, let's say you'd like to have your <EM>.signature</EM> file be a named pipe that has a Perl program on the other end. Now every
time any program (like a mailer, news reader, finger program, etc.) tries
to read from that file, the reading program will block and your program
will supply the new signature. We'll use the pipe-checking file test <STRONG>-p</STRONG>
to find out whether anyone (or anything) has accidentally removed our fifo.


<P>

<PRE>    chdir; # go home
    $FIFO = '.signature';
    $ENV{PATH} .= &quot;:/etc:/usr/games&quot;;
</PRE>

<P>

<PRE>    while (1) {
        unless (-p $FIFO) {
            unlink $FIFO;
            system('mknod', $FIFO, 'p')
                &amp;&amp; die &quot;can't mknod $FIFO: $!&quot;;
        }
</PRE>

<P>

<PRE>        # next line blocks until there's a reader
        open (FIFO, &quot;&gt; $FIFO&quot;) || die &quot;can't write $FIFO: $!&quot;;
        print FIFO &quot;John Smith (smith\@host.org)\n&quot;, `fortune -s`;
        close FIFO;
        sleep 2;    # to avoid dup signals
    }
</PRE>

<P>

<P>
<HR>
<H1><A NAME="Using_open_for_IPC">Using open() for IPC

</A></H1>
Perl's basic <CODE>open()</CODE> statement can also be used for
unidirectional interprocess communication by either appending or prepending
a pipe symbol to the second argument to <CODE>open().</CODE> Here's how to
start something up in a child process you intend to write to:


<P>

<PRE>    open(SPOOLER, &quot;| cat -v | lpr -h 2&gt;/dev/null&quot;)
                    || die &quot;can't fork: $!&quot;;
    local $SIG{PIPE} = sub { die &quot;spooler pipe broke&quot; };
    print SPOOLER &quot;stuff\n&quot;;
    close SPOOLER || die &quot;bad spool: $! $?&quot;;
</PRE>

<P>

And here's how to start up a child process you intend to read from:


<P>

<PRE>    open(STATUS, &quot;netstat -an 2&gt;&amp;1 |&quot;)
                    || die &quot;can't fork: $!&quot;;
    while (&lt;STATUS&gt;) {
        next if /^(tcp|udp)/;
        print;
    }
    close STATUS || die &quot;bad netstat: $! $?&quot;;
</PRE>

<P>

If one can be sure that a particular program is a Perl script that is expecting filenames in 
<FONT SIZE=-1>@ARGV,</FONT> the clever programmer can write something like this:



<P>

<PRE>    $ program f1 &quot;cmd1|&quot; - f2 &quot;cmd2|&quot; f3 &lt; tmpfile
</PRE>

<P>

and irrespective of which shell it's called from, the Perl program will
read from the file <EM>f1</EM>, the process <EM>cmd1</EM>, standard input (<EM>tmpfile</EM>
in this case), the <EM>f2</EM> file, the <EM>cmd2</EM> command, and finally the <EM>f3</EM>
file. Pretty nifty, eh?


<P>

You might notice that you could use backticks for much the same effect as
opening a pipe for reading:


<P>

<PRE>    print grep { !/^(tcp|udp)/ } `netstat -an 2&gt;&amp;1`;
    die &quot;bad netstat&quot; if $?;
</PRE>

<P>

While this is true on the surface, it's much more efficient to process the
file one line or record at a time because then you don't have to read the
whole thing into memory at once. It also gives you finer control of the
whole process, letting you to kill off the child process early if you'd
like.


<P>

Be careful to check both the <CODE>open()</CODE> and the
<CODE>close()</CODE> return values. If you're <EM>writing</EM> to a pipe, you should also trap 
<FONT SIZE=-1>SIGPIPE.</FONT> Otherwise, think of what happens when you
start up a pipe to a command that doesn't exist: the <CODE>open()</CODE>
will in all likelihood succeed (it only reflects the <CODE>fork()'s</CODE>
success), but then your output will fail--spectacularly. Perl can't know
whether the command worked because your command is actually running in a
separate process whose <CODE>exec()</CODE> might have failed. Therefore,
while readers of bogus commands return just a quick end of file, writers to
bogus command will trigger a signal they'd better be prepared to handle.
Consider:


<P>

<PRE>    open(FH, &quot;|bogus&quot;);
    print FH &quot;bang\n&quot;;
    close FH;
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Filehandles">Filehandles

</A></H2>
Both the main process and the child process share the same 
<FONT SIZE=-1>STDIN,</FONT> 
<FONT SIZE=-1>STDOUT</FONT> and 
<FONT SIZE=-1>STDERR</FONT> filehandles. If both processes try to access them at once, strange things can happen. You may want to close or reopen the filehandles for the child. You can get around this by opening your pipe with <CODE>open(),</CODE> but on some systems this means that the child process cannot outlive the parent.



<P>

<P>
<HR>
<H2><A NAME="Background_Processes">Background Processes

</A></H2>
You can run a command in the background with:


<P>

<PRE>    system(&quot;cmd &amp;&quot;);
</PRE>

<P>

The command's 
<FONT SIZE=-1>STDOUT</FONT> and 
<FONT SIZE=-1>STDERR</FONT> (and possibly 
<FONT SIZE=-1>STDIN,</FONT> depending on your shell) will be the same as the parent's. You won't need to catch 
<FONT SIZE=-1>SIGCHLD</FONT> because of the double-fork taking place (see below for more details).



<P>

<P>
<HR>
<H2><A NAME="Complete_Dissociation_of_Child_f">Complete Dissociation of Child from Parent

</A></H2>
In some cases (starting server processes, for instance) you'll want to
complete dissociate the child process from the parent. The following
process is reported to work on most Unixish systems. Non-Unix users should
check their Your_OS::Process module for other solutions.


<P>

<UL>
<LI><STRONG></STRONG>
Open /dev/tty and use the 
<FONT SIZE=-1>TIOCNOTTY</FONT> ioctl on it. See <EM>tty(4)</EM>
for details.


<P>

<LI><STRONG></STRONG>
Change directory to /


<P>

<LI><STRONG></STRONG>
Reopen 
<FONT SIZE=-1>STDIN,</FONT> 
<FONT SIZE=-1>STDOUT,</FONT> and 
<FONT SIZE=-1>STDERR</FONT> so they're not connected to the old tty.



<P>

<LI><STRONG></STRONG>
Background yourself like this:


<P>

<PRE>    fork &amp;&amp; exit;
</PRE>

<P>

</UL>
<P>
<HR>
<H2><A NAME="Safe_Pipe_Opens">Safe Pipe Opens

</A></H2>
Another interesting approach to 
<FONT SIZE=-1>IPC</FONT> is making your single program go multiprocess and
communicate between (or even amongst) yourselves. The <CODE>open()</CODE>
function will accept a file argument of either <CODE>"-|"</CODE> or <CODE>"|-"</CODE>
to do a very interesting thing: it forks a child connected to the filehandle you've opened. The child is running the same program as the parent. This is useful for safely opening a file when running under an assumed 
<FONT SIZE=-1>UID</FONT> or 
<FONT SIZE=-1>GID,</FONT> for example. If you open a pipe
 <EM>to</EM> minus, you can write to the filehandle you opened and your kid will find it in his 
<FONT SIZE=-1>STDIN.</FONT> If you open a pipe
 <EM>from</EM> minus, you can read from the filehandle you opened whatever your kid writes to his 
<FONT SIZE=-1>STDOUT.</FONT>



<P>

<PRE>    use English;
    my $sleep_count = 0;
</PRE>

<P>

<PRE>    do {
        $pid = open(KID_TO_WRITE, &quot;|-&quot;);
        unless (defined $pid) {
            warn &quot;cannot fork: $!&quot;;
            die &quot;bailing out&quot; if $sleep_count++ &gt; 6;
            sleep 10;
        }
    } until defined $pid;
</PRE>

<P>

<PRE>    if ($pid) {  # parent
        print KID_TO_WRITE @some_data;
        close(KID_TO_WRITE) || warn &quot;kid exited $?&quot;;
    } else {     # child
        ($EUID, $EGID) = ($UID, $GID); # suid progs only
        open (FILE, &quot;&gt; /safe/file&quot;)
            || die &quot;can't open /safe/file: $!&quot;;
        while (&lt;STDIN&gt;) {
            print FILE; # child's STDIN is parent's KID
        }
        exit;  # don't forget this
    }
</PRE>

<P>

Another common use for this construct is when you need to execute something
without the shell's interference. With <CODE>system(),</CODE> it's
straightforward, but you can't use a pipe open or backticks safely. That's
because there's no way to stop the shell from getting its hands on your
arguments. Instead, use lower-level control to call <CODE>exec()</CODE>
directly.


<P>

Here's a safe backtick or pipe open for read:


<P>

<PRE>    # add error processing as above
    $pid = open(KID_TO_READ, &quot;-|&quot;);
</PRE>

<P>

<PRE>    if ($pid) {   # parent
        while (&lt;KID_TO_READ&gt;) {
            # do something interesting
        }
        close(KID_TO_READ) || warn &quot;kid exited $?&quot;;
</PRE>

<P>

<PRE>    } else {      # child
        ($EUID, $EGID) = ($UID, $GID); # suid only
        exec($program, @options, @args)
            || die &quot;can't exec program: $!&quot;;
        # NOTREACHED
    }
</PRE>

<P>

And here's a safe pipe open for writing:


<P>

<PRE>    # add error processing as above
    $pid = open(KID_TO_WRITE, &quot;|-&quot;);
    $SIG{ALRM} = sub { die &quot;whoops, $program pipe broke&quot; };
</PRE>

<P>

<PRE>    if ($pid) {  # parent
        for (@data) {
            print KID_TO_WRITE;
        }
        close(KID_TO_WRITE) || warn &quot;kid exited $?&quot;;
</PRE>

<P>

<PRE>    } else {     # child
        ($EUID, $EGID) = ($UID, $GID);
        exec($program, @options, @args)
            || die &quot;can't exec program: $!&quot;;
        # NOTREACHED
    }
</PRE>

<P>

Note that these operations are full Unix forks, which means they may not be
correctly implemented on alien systems. Additionally, these are not true
multithreading. If you'd like to learn more about threading, see the
<EM>modules</EM> file mentioned below in the 
<FONT SIZE=-1>SEE</FONT> 
<FONT SIZE=-1>ALSO</FONT> section.



<P>

<P>
<HR>
<H2><A NAME="Bidirectional_Communication_with">Bidirectional Communication with Another Process

</A></H2>
While this works reasonably well for unidirectional communication, what
about bidirectional communication? The obvious thing you'd like to do
doesn't actually work:


<P>

<PRE>    open(PROG_FOR_READING_AND_WRITING, &quot;| some program |&quot;)
</PRE>

<P>

and if you forget to use the <STRONG>-w</STRONG> flag, then you'll miss out entirely on the diagnostic message:


<P>

<PRE>    Can't do bidirectional pipe at -e line 1.
</PRE>

<P>

If you really want to, you can use the standard <CODE>open2()</CODE> library function to catch both ends. There's also an <CODE>open3()</CODE> for tridirectional 
<FONT SIZE=-1>I/O</FONT> so you can also catch your child's 
<FONT SIZE=-1>STDERR,</FONT> but doing so would then require an awkward <CODE>select()</CODE> loop and wouldn't allow you to use normal Perl input operations.



<P>

If you look at its source, you'll see that <CODE>open2()</CODE> uses low-level primitives like Unix <CODE>pipe()</CODE> and <CODE>exec()</CODE> to create all the connections. While it might have been slightly more efficient by using <CODE>socketpair(),</CODE> it would have then been even less portable than it already is. The <CODE>open2()</CODE> and <CODE>open3()</CODE> functions are unlikely to work anywhere except on a Unix system or some other one purporting to be 
<FONT SIZE=-1>POSIX</FONT> compliant.



<P>

Here's an example of using <CODE>open2():</CODE>


<P>

<PRE>    use FileHandle;
    use IPC::Open2;
    $pid = open2( \*Reader, \*Writer, &quot;cat -u -n&quot; );
    Writer-&gt;autoflush(); # default here, actually
    print Writer &quot;stuff\n&quot;;
    $got = &lt;Reader&gt;;
</PRE>

<P>

The problem with this is that Unix buffering is really going to ruin your
day. Even though your <CODE>Writer</CODE> filehandle is auto-flushed, and the process on the other end will get your
data in a timely manner, you can't usually do anything to force it to give
it back to you in a similarly quick fashion. In this case, we could,
because we gave <EM>cat</EM> a <STRONG>-u</STRONG> flag to make it unbuffered. But very few Unix commands are designed to
operate over pipes, so this seldom works unless you yourself wrote the
program on the other end of the double-ended pipe.


<P>


<FONT SIZE=-1>A</FONT> solution to this is the nonstandard <EM>Comm.pl</EM> library. It uses pseudo-ttys to make your program behave more reasonably:


<P>

<PRE>    require 'Comm.pl';
    $ph = open_proc('cat -n');
    for (1..10) {
        print $ph &quot;a line\n&quot;;
        print &quot;got back &quot;, scalar &lt;$ph&gt;;
    }
</PRE>

<P>

This way you don't have to have control over the source code of the program
you're using. The <EM>Comm</EM> library also has <CODE>expect()</CODE> and <CODE>interact()</CODE>
functions. Find the library (and we hope its successor <EM>IPC::Chat</EM>) at your nearest 
<FONT SIZE=-1>CPAN</FONT> archive as detailed in the 
<FONT SIZE=-1>SEE</FONT> 
<FONT SIZE=-1>ALSO</FONT> section below.



<P>

<P>
<HR>
<H1><A NAME="Sockets_Client_Server_Communica">Sockets: Client/Server Communication

</A></H1>
While not limited to Unix-derived operating systems (e.g., WinSock on PCs provides socket support, as do some 
<FONT SIZE=-1>VMS</FONT> libraries), you may not have sockets on your system, in which case this section probably isn't going to do you much good. With sockets, you can do both virtual circuits (i.e., 
<FONT SIZE=-1>TCP</FONT> streams) and datagrams (i.e., 
<FONT SIZE=-1>UDP</FONT> packets). You may be able to do even more depending on your system.



<P>

The Perl function calls for dealing with sockets have the same names as the corresponding system calls in 
<FONT SIZE=-1>C,</FONT> but their arguments tend to differ for two reasons: first, Perl filehandles work differently than 
<FONT SIZE=-1>C</FONT> file descriptors. Second, Perl already knows the length of its strings, so you don't need to pass that information.



<P>

One of the major problems with old socket code in Perl was that it used
hard-coded values for some of the constants, which severely hurt
portability. If you ever see code that does anything like explicitly
setting <CODE>$AF_INET = 2</CODE>, you know you're in for big trouble: An immeasurably superior approach is
to use the <CODE>Socket</CODE> module, which more reliably grants access to various constants and
functions you'll need.


<P>

If you're not writing a server/client for an existing protocol like 
<FONT SIZE=-1>NNTP</FONT> or 
<FONT SIZE=-1>SMTP,</FONT> you should give some thought to how your server will know when the client has finished talking, and vice-versa. Most protocols are based on one-line messages and responses (so one party knows the other has finished when a ``\n'' is received) or multi-line messages and responses that end with a period on an empty line (``\n.\n'' terminates a message/response).



<P>

<P>
<HR>
<H2><A NAME="Internet_TCP_Clients_and_Servers">Internet TCP Clients and Servers

</A></H2>
Use Internet-domain sockets when you want to do client-server communication
that might extend to machines outside of your own system.


<P>

Here's a sample 
<FONT SIZE=-1>TCP</FONT> client using Internet-domain sockets:


<P>

<PRE>    #!/usr/bin/perl -w
    require 5.002;
    use strict;
    use Socket;
    my ($remote,$port, $iaddr, $paddr, $proto, $line);
</PRE>

<P>

<PRE>    $remote  = shift || 'localhost';
    $port    = shift || 2345;  # random port
    if ($port =~ /\D/) { $port = getservbyname($port, 'tcp') }
    die &quot;No port&quot; unless $port;
    $iaddr   = inet_aton($remote)               || die &quot;no host: $remote&quot;;
    $paddr   = sockaddr_in($port, $iaddr);
</PRE>

<P>

<PRE>    $proto   = getprotobyname('tcp');
    socket(SOCK, PF_INET, SOCK_STREAM, $proto)  || die &quot;socket: $!&quot;;
    connect(SOCK, $paddr)    || die &quot;connect: $!&quot;;
    while (defined($line = &lt;SOCK&gt;)) {
        print $line;
    }
</PRE>

<P>

<PRE>    close (SOCK)            || die &quot;close: $!&quot;;
    exit;
</PRE>

<P>

And here's a corresponding server to go along with it. We'll leave the address as 
<FONT SIZE=-1>INADDR_ANY</FONT> so that the kernel can choose the appropriate interface on multihomed hosts. If you want sit on a particular interface (like the external side of a gateway or firewall machine), you should fill this in with your real address instead.



<P>

<PRE>    #!/usr/bin/perl -Tw
    require 5.002;
    use strict;
    BEGIN { $ENV{PATH} = '/usr/ucb:/bin' }
    use Socket;
    use Carp;
</PRE>

<P>

<PRE>    sub logmsg { print &quot;$0 $$: @_ at &quot;, scalar localtime, &quot;\n&quot; }
</PRE>

<P>

<PRE>    my $port = shift || 2345;
    my $proto = getprotobyname('tcp');
    $port = $1 if $port =~ /(\d+)/; # untaint port number
</PRE>

<P>

<PRE>    socket(Server, PF_INET, SOCK_STREAM, $proto)        || die &quot;socket: $!&quot;;
    setsockopt(Server, SOL_SOCKET, SO_REUSEADDR,
                                        pack(&quot;l&quot;, 1))   || die &quot;setsockopt: $!&quot;;
    bind(Server, sockaddr_in($port, INADDR_ANY))        || die &quot;bind: $!&quot;;
    listen(Server,SOMAXCONN)                            || die &quot;listen: $!&quot;;
</PRE>

<P>

<PRE>    logmsg &quot;server started on port $port&quot;;
</PRE>

<P>

<PRE>    my $paddr;
</PRE>

<P>

<PRE>    $SIG{CHLD} = \&amp;REAPER;
</PRE>

<P>

<PRE>    for ( ; $paddr = accept(Client,Server); close Client) {
        my($port,$iaddr) = sockaddr_in($paddr);
        my $name = gethostbyaddr($iaddr,AF_INET);
</PRE>

<P>

<PRE>        logmsg &quot;connection from $name [&quot;,
                inet_ntoa($iaddr), &quot;]
                at port $port&quot;;
</PRE>

<P>

<PRE>        print Client &quot;Hello there, $name, it's now &quot;,
                        scalar localtime, &quot;\n&quot;;
    }
</PRE>

<P>

And here's a multithreaded version. It's multithreaded in that like most
typical servers, it spawns (forks) a slave server to handle the client
request so that the master server can quickly go back to service a new
client.


<P>

<PRE>    #!/usr/bin/perl -Tw
    require 5.002;
    use strict;
    BEGIN { $ENV{PATH} = '/usr/ucb:/bin' }
    use Socket;
    use Carp;
</PRE>

<P>

<PRE>    sub spawn;  # forward declaration
    sub logmsg { print &quot;$0 $$: @_ at &quot;, scalar localtime, &quot;\n&quot; }
</PRE>

<P>

<PRE>    my $port = shift || 2345;
    my $proto = getprotobyname('tcp');
    $port = $1 if $port =~ /(\d+)/; # untaint port number
</PRE>

<P>

<PRE>    socket(Server, PF_INET, SOCK_STREAM, $proto)        || die &quot;socket: $!&quot;;
    setsockopt(Server, SOL_SOCKET, SO_REUSEADDR,
                                        pack(&quot;l&quot;, 1))   || die &quot;setsockopt: $!&quot;;
    bind(Server, sockaddr_in($port, INADDR_ANY))        || die &quot;bind: $!&quot;;
    listen(Server,SOMAXCONN)                            || die &quot;listen: $!&quot;;
</PRE>

<P>

<PRE>    logmsg &quot;server started on port $port&quot;;
</PRE>

<P>

<PRE>    my $waitedpid = 0;
    my $paddr;
</PRE>

<P>

<PRE>    sub REAPER {
        $waitedpid = wait;
        $SIG{CHLD} = \&amp;REAPER;  # loathe sysV
        logmsg &quot;reaped $waitedpid&quot; . ($? ? &quot; with exit $?&quot; : '');
    }
</PRE>

<P>

<PRE>    $SIG{CHLD} = \&amp;REAPER;
</PRE>

<P>

<PRE>    for ( $waitedpid = 0;
          ($paddr = accept(Client,Server)) || $waitedpid;
          $waitedpid = 0, close Client)
    {
        next if $waitedpid and not $paddr;
        my($port,$iaddr) = sockaddr_in($paddr);
        my $name = gethostbyaddr($iaddr,AF_INET);
</PRE>

<P>

<PRE>        logmsg &quot;connection from $name [&quot;,
                inet_ntoa($iaddr), &quot;]
                at port $port&quot;;
</PRE>

<P>

<PRE>        spawn sub {
            print &quot;Hello there, $name, it's now &quot;, scalar localtime, &quot;\n&quot;;
            exec '/usr/games/fortune'
                or confess &quot;can't exec fortune: $!&quot;;
        };
</PRE>

<P>

<PRE>    }
</PRE>

<P>

<PRE>    sub spawn {
        my $coderef = shift;
</PRE>

<P>

<PRE>        unless (@_ == 0 &amp;&amp; $coderef &amp;&amp; ref($coderef) eq 'CODE') {
            confess &quot;usage: spawn CODEREF&quot;;
        }
</PRE>

<P>

<PRE>        my $pid;
        if (!defined($pid = fork)) {
            logmsg &quot;cannot fork: $!&quot;;
            return;
        } elsif ($pid) {
            logmsg &quot;begat $pid&quot;;
            return; # I'm the parent
        }
        # else I'm the child -- go spawn
</PRE>

<P>

<PRE>        open(STDIN,  &quot;&lt;&amp;Client&quot;)   || die &quot;can't dup client to stdin&quot;;
        open(STDOUT, &quot;&gt;&amp;Client&quot;)   || die &quot;can't dup client to stdout&quot;;
        ## open(STDERR, &quot;&gt;&amp;STDOUT&quot;) || die &quot;can't dup stdout to stderr&quot;;
        exit &amp;$coderef();
    }
</PRE>

<P>

This server takes the trouble to clone off a child version via
<CODE>fork()</CODE> for each incoming request. That way it can handle many
requests at once, which you might not always want. Even if you don't
<CODE>fork(),</CODE> the <CODE>listen()</CODE> will allow that many pending
connections. Forking servers have to be particularly careful about cleaning
up their dead children (called ``zombies'' in Unix parlance), because
otherwise you'll quickly fill up your process table.


<P>

We suggest that you use the <STRONG>-T</STRONG> flag to use taint checking (see <A HREF="/n|/perl/html/./lib/Pod/perlsec.html#">the perlsec manpage</A>) even if we aren't running setuid or setgid. This is always a good idea for servers and other programs run on behalf of someone else (like 
<FONT SIZE=-1>CGI</FONT> scripts), because it lessens the chances that people from the outside will be able to compromise your system.



<P>

Let's look at another 
<FONT SIZE=-1>TCP</FONT> client. This one connects to the 
<FONT SIZE=-1>TCP</FONT> ``time'' service on a number of different machines and shows how far their clocks differ from the system on which it's being run:



<P>

<PRE>    #!/usr/bin/perl  -w
    require 5.002;
    use strict;
    use Socket;
</PRE>

<P>

<PRE>    my $SECS_of_70_YEARS = 2208988800;
    sub ctime { scalar localtime(shift) }
</PRE>

<P>

<PRE>    my $iaddr = gethostbyname('localhost');
    my $proto = getprotobyname('tcp');
    my $port = getservbyname('time', 'tcp');
    my $paddr = sockaddr_in(0, $iaddr);
    my($host);
</PRE>

<P>

<PRE>    $| = 1;
    printf &quot;%-24s %8s %s\n&quot;,  &quot;localhost&quot;, 0, ctime(time());
</PRE>

<P>

<PRE>    foreach $host (@ARGV) {
        printf &quot;%-24s &quot;, $host;
        my $hisiaddr = inet_aton($host)     || die &quot;unknown host&quot;;
        my $hispaddr = sockaddr_in($port, $hisiaddr);
        socket(SOCKET, PF_INET, SOCK_STREAM, $proto)   || die &quot;socket: $!&quot;;
        connect(SOCKET, $hispaddr)          || die &quot;bind: $!&quot;;
        my $rtime = '    ';
        read(SOCKET, $rtime, 4);
        close(SOCKET);
        my $histime = unpack(&quot;N&quot;, $rtime) - $SECS_of_70_YEARS ;
        printf &quot;%8d %s\n&quot;, $histime - time, ctime($histime);
    }
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Unix_Domain_TCP_Clients_and_Serv">Unix-Domain TCP Clients and Servers

</A></H2>
That's fine for Internet-domain clients and servers, but what about local
communications? While you can use the same setup, sometimes you don't want
to. Unix-domain sockets are local to the current host, and are often used
internally to implement pipes. Unlike Internet domain sockets, Unix domain
sockets can show up in the file system with an <CODE>ls(1)</CODE> listing.


<P>

<PRE>    $ ls -l /dev/log
    srw-rw-rw-  1 root            0 Oct 31 07:23 /dev/log
</PRE>

<P>

You can test for these with Perl's <STRONG>-S</STRONG> file test:


<P>

<PRE>    unless ( -S '/dev/log' ) {
        die &quot;something's wicked with the print system&quot;;
    }
</PRE>

<P>

Here's a sample Unix-domain client:


<P>

<PRE>    #!/usr/bin/perl -w
    require 5.002;
    use Socket;
    use strict;
    my ($rendezvous, $line);
</PRE>

<P>

<PRE>    $rendezvous = shift || '/tmp/catsock';
    socket(SOCK, PF_UNIX, SOCK_STREAM, 0)       || die &quot;socket: $!&quot;;
    connect(SOCK, sockaddr_un($rendezvous))     || die &quot;connect: $!&quot;;
    while (defined($line = &lt;SOCK&gt;)) {
        print $line;
    }
    exit;
</PRE>

<P>

And here's a corresponding server.


<P>

<PRE>    #!/usr/bin/perl -Tw
    require 5.002;
    use strict;
    use Socket;
    use Carp;
</PRE>

<P>

<PRE>    BEGIN { $ENV{PATH} = '/usr/ucb:/bin' }
</PRE>

<P>

<PRE>    my $NAME = '/tmp/catsock';
    my $uaddr = sockaddr_un($NAME);
    my $proto = getprotobyname('tcp');
</PRE>

<P>

<PRE>    socket(Server,PF_UNIX,SOCK_STREAM,0)        || die &quot;socket: $!&quot;;
    unlink($NAME);
    bind  (Server, $uaddr)                      || die &quot;bind: $!&quot;;
    listen(Server,SOMAXCONN)                    || die &quot;listen: $!&quot;;
</PRE>

<P>

<PRE>    logmsg &quot;server started on $NAME&quot;;
</PRE>

<P>

<PRE>    $SIG{CHLD} = \&amp;REAPER;
</PRE>

<P>

<PRE>    for ( $waitedpid = 0;
          accept(Client,Server) || $waitedpid;
          $waitedpid = 0, close Client)
    {
        next if $waitedpid;
        logmsg &quot;connection on $NAME&quot;;
        spawn sub {
            print &quot;Hello there, it's now &quot;, scalar localtime, &quot;\n&quot;;
            exec '/usr/games/fortune' or die &quot;can't exec fortune: $!&quot;;
        };
    }
</PRE>

<P>

As you see, it's remarkably similar to the Internet domain 
<FONT SIZE=-1>TCP</FONT> server, so much so, in fact, that we've omitted
several duplicate functions--spawn(), <CODE>logmsg(),</CODE>
<CODE>ctime(),</CODE> and <CODE>REAPER()--which</CODE> are exactly the same
as in the other server.


<P>

So why would you ever want to use a Unix domain socket instead of a simpler
named pipe? Because a named pipe doesn't give you sessions. You can't tell
one process's data from another's. With socket programming, you get a
separate session for each client: that's why <CODE>accept()</CODE> takes
two arguments.


<P>

For example, let's say that you have a long running database server daemon that you want folks from the World Wide Web to be able to access, but only if they go through a 
<FONT SIZE=-1>CGI</FONT> interface. You'd have a small, simple 
<FONT SIZE=-1>CGI</FONT> program that does whatever checks and logging you feel like, and then acts as a Unix-domain client and connects to your private server.



<P>

<P>
<HR>
<H1><A NAME="TCP_Clients_with_IO_Socket">TCP Clients with IO::Socket

</A></H1>
For those preferring a higher-level interface to socket programming, the IO::Socket module provides an object-oriented approach. IO::Socket is included as part of the standard Perl distribution as of the 5.004 release. If you're running an earlier version of Perl, just fetch IO::Socket from 
<FONT SIZE=-1>CPAN,</FONT> where you'll also find find modules providing easy interfaces to the following systems: 
<FONT SIZE=-1>DNS,</FONT> 
<FONT SIZE=-1>FTP,</FONT> Ident 
<FONT SIZE=-1>(RFC</FONT> 931), 
<FONT SIZE=-1>NIS</FONT> and NISPlus, 
<FONT SIZE=-1>NNTP,</FONT> Ping, 
<FONT SIZE=-1>POP3,</FONT> 
<FONT SIZE=-1>SMTP,</FONT> 
<FONT SIZE=-1>SNMP,</FONT> SSLeay, Telnet, and Time--just to name a few.



<P>

<P>
<HR>
<H2><A NAME="A_Simple_Client">A Simple Client

</A></H2>
Here's a client that creates a 
<FONT SIZE=-1>TCP</FONT> connection to the ``daytime'' service at port 13
of the host name ``localhost'' and prints out everything that the server
there cares to provide.


<P>

<PRE>    #!/usr/bin/perl -w
    use IO::Socket;
    $remote = IO::Socket::INET-&gt;new(
                        Proto    =&gt; &quot;tcp&quot;,
                        PeerAddr =&gt; &quot;localhost&quot;,
                        PeerPort =&gt; &quot;daytime(13)&quot;,
                    )
                  or die &quot;cannot connect to daytime port at localhost&quot;;
    while ( &lt;$remote&gt; ) { print }
</PRE>

<P>

When you run this program, you should get something back that looks like
this:


<P>

<PRE>    Wed May 14 08:40:46 MDT 1997
</PRE>

<P>

Here are what those parameters to the <A HREF="#item_new">new</A> constructor mean:


<P>

<DL>
<DT><STRONG><A NAME="item_Proto">Proto

</A></STRONG><DD>
This is which protocol to use. In this case, the socket handle returned will be connected to a 
<FONT SIZE=-1>TCP</FONT> socket, because we want a stream-oriented connection, that is, one that acts pretty much like a plain old file. Not all sockets are this of this type. For example, the 
<FONT SIZE=-1>UDP</FONT> protocol can be used to make a datagram socket, used for message-passing.



<P>

<DT><STRONG><A NAME="item_PeerAddr">PeerAddr

</A></STRONG><DD>
This is the name or Internet address of the remote host the server is
running on. We could have specified a longer name like <CODE>"www.perl.com"</CODE>, or an address like <CODE>"204.148.40.9"</CODE>. For demonstration purposes, we've used the special hostname <CODE>"localhost"</CODE>, which should always mean the current machine you're running on. The
corresponding Internet address for localhost is <CODE>"127.1"</CODE>, if you'd rather use that.


<P>

<DT><STRONG><A NAME="item_PeerPort">PeerPort

</A></STRONG><DD>
This is the service name or port number we'd like to connect to. We could
have gotten away with using just <CODE>"daytime"</CODE> on systems with a well-configured system services file,[FOOTNOTE: The
system services file is in <EM>/etc/services</EM> under Unix] but just in case, we've specified the port number (13) in
parentheses. Using just the number would also have worked, but constant
numbers make careful programmers nervous.


<P>

</DL>
Notice how the return value from the <A HREF="#item_new">new</A> constructor is used as a filehandle in the <CODE>while</CODE> loop? That's what's called an indirect filehandle, a scalar variable
containing a filehandle. You can use it the same way you would a normal
filehandle. For example, you can read one line from it this way:


<P>

<PRE>    $line = &lt;$handle&gt;;
</PRE>

<P>

all remaining lines from is this way:


<P>

<PRE>    @lines = &lt;$handle&gt;;
</PRE>

<P>

and send a line of data to it this way:


<P>

<PRE>    print $handle &quot;some data\n&quot;;
</PRE>

<P>

<P>
<HR>
<H2><A NAME="A_Webget_Client">A Webget Client

</A></H2>
Here's a simple client that takes a remote host to fetch a document from,
and then a list of documents to get from that host. This is a more
interesting client than the previous one because it first sends something
to the server before fetching the server's response.


<P>

<PRE>    #!/usr/bin/perl -w
    use IO::Socket;
    unless (@ARGV &gt; 1) { die &quot;usage: $0 host document ...&quot; }
    $host = shift(@ARGV);
    foreach $document ( @ARGV ) {
        $remote = IO::Socket::INET-&gt;new( Proto     =&gt; &quot;tcp&quot;,
                                         PeerAddr  =&gt; $host,
                                         PeerPort  =&gt; &quot;http(80)&quot;,
                                        );
        unless ($remote) { die &quot;cannot connect to http daemon on $host&quot; }
        $remote-&gt;autoflush(1);
        print $remote &quot;GET $document HTTP/1.0\n\n&quot;;
        while ( &lt;$remote&gt; ) { print }
        close $remote;
    }
</PRE>

<P>

The web server handing the ``http'' service, which is assumed to be at its
standard port, number 80. If your the web server you're trying to connect
to is at a different port (like 1080 or 8080), you should specify as the
named-parameter pair, <CODE>PeerPort =&gt; 8080</CODE>. The <A HREF="/n|/perl/html/./lib/Pod/perlvar.html#item_autoflush">autoflush</A>
method is used on the socket because otherwise the system would buffer up
the output we sent it. (If you're on a Mac, you'll also need to change
every <A HREF="#item__n_">"\n"</A> in your code that sends data over the network to be a <CODE>"\015\012"</CODE> instead.)


<P>

Connecting to the server is only the first part of the process: once you have the connection, you have to use the server's language. Each server on the network has its own little command language that it expects as input. The string that we send to the server starting with 
<FONT SIZE=-1>``GET''</FONT> is in 
<FONT SIZE=-1>HTTP</FONT> syntax. In this case, we simply request each specified document. Yes, we really are making a new connection for each document, even though it's the same host. That's the way you always used to have to speak 
<FONT SIZE=-1>HTTP.</FONT> Recent versions of web browsers may request that the remote server leave the connection open a little while, but the server doesn't have to honor such a request.



<P>

Here's an example of running that program, which we'll call <EM>webget</EM>:


<P>

<PRE>    shell_prompt$ webget www.perl.com /guanaco.html
    HTTP/1.1 404 File Not Found
    Date: Thu, 08 May 1997 18:02:32 GMT
    Server: Apache/1.2b6
    Connection: close
    Content-type: text/html
</PRE>

<P>

<PRE>    &lt;HEAD&gt;&lt;TITLE&gt;404 File Not Found&lt;/TITLE&gt;&lt;/HEAD&gt;
    &lt;BODY&gt;&lt;H1&gt;File Not Found&lt;/H1&gt;
    The requested URL /guanaco.html was not found on this server.&lt;P&gt;
    &lt;/BODY&gt;
</PRE>

<P>

Ok, so that's not very interesting, because it didn't find that particular
document. But a long response wouldn't have fit on this page.


<P>

For a more fully-featured version of this program, you should look to the <EM>lwp-request</EM> program included with the 
<FONT SIZE=-1>LWP</FONT> modules from 
<FONT SIZE=-1>CPAN.</FONT>



<P>

<P>
<HR>
<H2><A NAME="Interactive_Client_with_IO_Sock">Interactive Client with IO::Socket

</A></H2>
Well, that's all fine if you want to send one command and get one answer,
but what about setting up something fully interactive, somewhat like the
way <EM>telnet</EM> works? That way you can type a line, get the answer, type a line, get the
answer, etc.


<P>

This client is more complicated than the two we've done so far, but if
you're on a system that supports the powerful <A HREF="#item_fork">fork</A> call, the solution isn't that rough. Once you've made the connection to
whatever service you'd like to chat with, call <A HREF="#item_fork">fork</A> to clone your process. Each of these two identical process has a very
simple job to do: the parent copies everything from the socket to standard
output, while the child simultaneously copies everything from standard
input to the socket. To accomplish the same thing using just one process
would be <EM>much</EM>
harder, because it's easier to code two processes to do one thing than it
is to code one process to do two things. (This keep-it-simple principle is
one of the cornerstones of the Unix philosophy, and good software
engineering as well, which is probably why it's spread to other systems as
well.)


<P>

Here's the code:


<P>

<PRE>    #!/usr/bin/perl -w
    use strict;
    use IO::Socket;
    my ($host, $port, $kidpid, $handle, $line);
</PRE>

<P>

<PRE>    unless (@ARGV == 2) { die &quot;usage: $0 host port&quot; }
    ($host, $port) = @ARGV;
</PRE>

<P>

<PRE>    # create a tcp connection to the specified host and port
    $handle = IO::Socket::INET-&gt;new(Proto     =&gt; &quot;tcp&quot;,
                                    PeerAddr  =&gt; $host,
                                    PeerPort  =&gt; $port)
           or die &quot;can't connect to port $port on $host: $!&quot;;
</PRE>

<P>

<PRE>    $handle-&gt;autoflush(1);              # so output gets there right away
    print STDERR &quot;[Connected to $host:$port]\n&quot;;
</PRE>

<P>

<PRE>    # split the program into two processes, identical twins
    die &quot;can't fork: $!&quot; unless defined($kidpid = fork());
</PRE>

<P>

<PRE>    # the if{} block runs only in the parent process
    if ($kidpid) {
        # copy the socket to standard output
        while (defined ($line = &lt;$handle&gt;)) {
            print STDOUT $line;
        }
        kill(&quot;TERM&quot;, $kidpid);                  # send SIGTERM to child
    }
    # the else{} block runs only in the child process
    else {
        # copy standard input to the socket
        while (defined ($line = &lt;STDIN&gt;)) {
            print $handle $line;
        }
    }
</PRE>

<P>

The <A HREF="#item_kill">kill</A> function in the parent's <CODE>if</CODE> block is there to send a signal to our child process (current running in
the <CODE>else</CODE> block) as soon as the remote server has closed its end of the connection.


<P>

The <A HREF="#item_kill">kill</A> at the end of the parent's block is there to eliminate the child process as
soon as the server we connect to closes its end.


<P>

If the remote server sends data a byte at time, and you need that data
immediately without waiting for a newline (which might not happen), you may
wish to replace the <CODE>while</CODE> loop in the parent with the following:


<P>

<PRE>    my $byte;
    while (sysread($handle, $byte, 1) == 1) {
        print STDOUT $byte;
    }
</PRE>

<P>

Making a system call for each byte you want to read is not very efficient
(to put it mildly) but is the simplest to explain and works reasonably
well.


<P>

<P>
<HR>
<H1><A NAME="TCP_Servers_with_IO_Socket">TCP Servers with IO::Socket

</A></H1>
Setting up server is little bit more involved than running a client. The
model is that the server creates a special kind of socket that does nothing
but listen on a particular port for incoming connections. It does this by
calling the <CODE>IO::Socket::INET-&gt;new()</CODE> method with slightly different arguments than the client did.


<P>

<DL>
<DT><STRONG>Proto

</A></STRONG><DD>
This is which protocol to use. Like our clients, we'll still specify <CODE>"tcp"</CODE> here.


<P>

<DT><STRONG><A NAME="item_LocalPort">LocalPort

</A></STRONG><DD>
We specify a local port in the <A HREF="#item_LocalPort">LocalPort</A> argument, which we didn't do for the client. This is service name or port
number for which you want to be the server. (Under Unix, ports under 1024
are restricted to the superuser.) In our sample, we'll use port 9000, but
you can use any port that's not currently in use on your system. If you try
to use one already in used, you'll get an ``Address already in use''
message. Under Unix, the <CODE>netstat -a</CODE> command will show which services current have servers.


<P>

<DT><STRONG><A NAME="item_Listen">Listen

</A></STRONG><DD>
The <A HREF="#item_Listen">Listen</A> parameter is set to the maximum number of pending connections we can accept until we turn away incoming clients. Think of it as a call-waiting queue for your telephone. The low-level Socket module has a special symbol for the system maximum, which is 
<FONT SIZE=-1>SOMAXCONN.</FONT>



<P>

<DT><STRONG><A NAME="item_Reuse">Reuse

</A></STRONG><DD>
The <A HREF="#item_Reuse">Reuse</A> parameter is needed so that we restart our server manually without waiting
a few minutes to allow system buffers to clear out.


<P>

</DL>
Once the generic server socket has been created using the parameters listed
above, the server then waits for a new client to connect to it. The server
blocks in the <A HREF="#item_accept">accept</A> method, which eventually an bidirectional connection to the remote client.
(Make sure to autoflush this handle to circumvent buffering.)


<P>

To add to user-friendliness, our server prompts the user for commands. Most
servers don't do this. Because of the prompt without a newline, you'll have
to use the <A HREF="#item_sysread">sysread</A> variant of the interactive client above.


<P>

This server accepts one of five different commands, sending output back to
the client. Note that unlike most network servers, this one only handles
one incoming client at a time. Multithreaded servers are covered in Chapter
6 of the Camel or in the <CODE>perlipc(1)</CODE> manpage.


<P>

Here's the code. We'll


<P>

<PRE> #!/usr/bin/perl -w
 use IO::Socket;
 use Net::hostent;              # for OO version of gethostbyaddr
</PRE>

<P>

<PRE> $PORT = 9000;                  # pick something not in use
</PRE>

<P>

<PRE> $server = IO::Socket::INET-&gt;new( Proto     =&gt; 'tcp',
                                  LocalPort =&gt; $PORT,
                                  Listen    =&gt; SOMAXCONN,
                                  Reuse     =&gt; 1);
</PRE>

<P>

<PRE> die &quot;can't setup server&quot; unless $server;
 print &quot;[Server $0 accepting clients]\n&quot;;
</PRE>

<P>

<PRE> while ($client = $server-&gt;accept()) {
   $client-&gt;autoflush(1);
   print $client &quot;Welcome to $0; type help for command list.\n&quot;;
   $hostinfo = gethostbyaddr($client-&gt;peeraddr);
   printf &quot;[Connect from %s]\n&quot;, $hostinfo-&gt;name || $client-&gt;peerhost;
   print $client &quot;Command? &quot;;
   while ( &lt;$client&gt;) {
     next unless /\S/;       # blank line
     if    (/quit|exit/i)    { last;                                     }
     elsif (/date|time/i)    { printf $client &quot;%s\n&quot;, scalar localtime;  }
     elsif (/who/i )         { print  $client `who 2&gt;&amp;1`;                }
     elsif (/cookie/i )      { print  $client `/usr/games/fortune 2&gt;&amp;1`; }
     elsif (/motd/i )        { print  $client `cat /etc/motd 2&gt;&amp;1`;      }
     else {
       print $client &quot;Commands: quit date who cookie motd\n&quot;;
     }
   } continue {
      print $client &quot;Command? &quot;;
   }
   close $client;
 }
</PRE>

<P>

<P>
<HR>
<H1><A NAME="UDP_Message_Passing">UDP: Message Passing

</A></H1>
Another kind of client-server setup is one that uses not connections, but messages. 
<FONT SIZE=-1>UDP</FONT> communications involve much lower overhead but also provide less reliability, as there are no promises that messages will arrive at all, let alone in order and unmangled. Still, 
<FONT SIZE=-1>UDP</FONT> offers some advantages over 
<FONT SIZE=-1>TCP,</FONT> including being able to ``broadcast'' or ``multicast'' to a whole bunch of destination hosts at once (usually on your local subnet). If you find yourself overly concerned about reliability and start building checks into your message system, then you probably should use just 
<FONT SIZE=-1>TCP</FONT> to start with.



<P>

Here's a 
<FONT SIZE=-1>UDP</FONT> program similar to the sample Internet 
<FONT SIZE=-1>TCP</FONT> client given earlier. However, instead of checking one host at a time, the 
<FONT SIZE=-1>UDP</FONT> version will check many of them asynchronously by simulating a multicast and then using <CODE>select()</CODE> to do a timed-out wait for 
<FONT SIZE=-1>I/O.</FONT> To do something similar with 
<FONT SIZE=-1>TCP,</FONT> you'd have to use a different socket handle for each host.



<P>

<PRE>    #!/usr/bin/perl -w
    use strict;
    require 5.002;
    use Socket;
    use Sys::Hostname;
</PRE>

<P>

<PRE>    my ( $count, $hisiaddr, $hispaddr, $histime,
         $host, $iaddr, $paddr, $port, $proto,
         $rin, $rout, $rtime, $SECS_of_70_YEARS);
</PRE>

<P>

<PRE>    $SECS_of_70_YEARS      = 2208988800;
</PRE>

<P>

<PRE>    $iaddr = gethostbyname(hostname());
    $proto = getprotobyname('udp');
    $port = getservbyname('time', 'udp');
    $paddr = sockaddr_in(0, $iaddr); # 0 means let kernel pick
</PRE>

<P>

<PRE>    socket(SOCKET, PF_INET, SOCK_DGRAM, $proto)   || die &quot;socket: $!&quot;;
    bind(SOCKET, $paddr)                          || die &quot;bind: $!&quot;;
</PRE>

<P>

<PRE>    $| = 1;
    printf &quot;%-12s %8s %s\n&quot;,  &quot;localhost&quot;, 0, scalar localtime time;
    $count = 0;
    for $host (@ARGV) {
        $count++;
        $hisiaddr = inet_aton($host)    || die &quot;unknown host&quot;;
        $hispaddr = sockaddr_in($port, $hisiaddr);
        defined(send(SOCKET, 0, 0, $hispaddr))    || die &quot;send $host: $!&quot;;
    }
</PRE>

<P>

<PRE>    $rin = '';
    vec($rin, fileno(SOCKET), 1) = 1;
</PRE>

<P>

<PRE>    # timeout after 10.0 seconds
    while ($count &amp;&amp; select($rout = $rin, undef, undef, 10.0)) {
        $rtime = '';
        ($hispaddr = recv(SOCKET, $rtime, 4, 0))        || die &quot;recv: $!&quot;;
        ($port, $hisiaddr) = sockaddr_in($hispaddr);
        $host = gethostbyaddr($hisiaddr, AF_INET);
        $histime = unpack(&quot;N&quot;, $rtime) - $SECS_of_70_YEARS ;
        printf &quot;%-12s &quot;, $host;
        printf &quot;%8d %s\n&quot;, $histime - time, scalar localtime($histime);
        $count--;
    }
</PRE>

<P>

<P>
<HR>
<H1><A NAME="SysV_IPC">SysV IPC

</A></H1>
While System 
<FONT SIZE=-1>V</FONT> 
<FONT SIZE=-1>IPC</FONT> isn't so widely used as sockets, it still has some interesting uses. You can't, however, effectively use SysV 
<FONT SIZE=-1>IPC</FONT> or Berkeley <CODE>mmap()</CODE> to have shared memory so as to share a variable amongst several processes. That's because Perl would reallocate your string when you weren't wanting it to.



<P>

Here's a small example showing shared memory usage.


<P>

<PRE>    $IPC_PRIVATE = 0;
    $IPC_RMID = 0;
    $size = 2000;
    $key = shmget($IPC_PRIVATE, $size , 0777 );
    die unless defined $key;
</PRE>

<P>

<PRE>    $message = &quot;Message #1&quot;;
    shmwrite($key, $message, 0, 60 ) || die &quot;$!&quot;;
    shmread($key,$buff,0,60) || die &quot;$!&quot;;
</PRE>

<P>

<PRE>    print $buff,&quot;\n&quot;;
</PRE>

<P>

<PRE>    print &quot;deleting $key\n&quot;;
    shmctl($key ,$IPC_RMID, 0) || die &quot;$!&quot;;
</PRE>

<P>

Here's an example of a semaphore:


<P>

<PRE>    $IPC_KEY = 1234;
    $IPC_RMID = 0;
    $IPC_CREATE = 0001000;
    $key = semget($IPC_KEY, $nsems , 0666 | $IPC_CREATE );
    die if !defined($key);
    print &quot;$key\n&quot;;
</PRE>

<P>

Put this code in a separate file to be run in more than one process. Call
the file <EM>take</EM>:


<P>

<PRE>    # create a semaphore
</PRE>

<P>

<PRE>    $IPC_KEY = 1234;
    $key = semget($IPC_KEY,  0 , 0 );
    die if !defined($key);
</PRE>

<P>

<PRE>    $semnum = 0;
    $semflag = 0;
</PRE>

<P>

<PRE>    # 'take' semaphore
    # wait for semaphore to be zero
    $semop = 0;
    $opstring1 = pack(&quot;sss&quot;, $semnum, $semop, $semflag);
</PRE>

<P>

<PRE>    # Increment the semaphore count
    $semop = 1;
    $opstring2 = pack(&quot;sss&quot;, $semnum, $semop,  $semflag);
    $opstring = $opstring1 . $opstring2;
</PRE>

<P>

<PRE>    semop($key,$opstring) || die &quot;$!&quot;;
</PRE>

<P>

Put this code in a separate file to be run in more than one process. Call
this file <EM>give</EM>:


<P>

<PRE>    # 'give' the semaphore
    # run this in the original process and you will see
    # that the second process continues
</PRE>

<P>

<PRE>    $IPC_KEY = 1234;
    $key = semget($IPC_KEY, 0, 0);
    die if !defined($key);
</PRE>

<P>

<PRE>    $semnum = 0;
    $semflag = 0;
</PRE>

<P>

<PRE>    # Decrement the semaphore count
    $semop = -1;
    $opstring = pack(&quot;sss&quot;, $semnum, $semop, $semflag);
</PRE>

<P>

<PRE>    semop($key,$opstring) || die &quot;$!&quot;;
</PRE>

<P>

The SysV 
<FONT SIZE=-1>IPC</FONT> code above was written long ago, and it's
definitely clunky looking. It should at the very least be made to <CODE>use strict</CODE>
and <CODE>require "sys/ipc.ph"</CODE>. Better yet, check out the IPC::SysV modules on 
<FONT SIZE=-1>CPAN.</FONT>


<P>

<P>
<HR>
<H1><A NAME="NOTES">NOTES

</A></H1>
If you are running under version 5.000 (dubious) or 5.001, you can still
use most of the examples in this document. You may have to remove the
<CODE>use strict</CODE> and some of the <CODE>my()</CODE> statements for 5.000, and for both you'll
have to load in version 1.2 or older of the <EM>Socket.pm</EM> module, which is included in <EM>perl5.002</EM>.


<P>

Most of these routines quietly but politely return <A HREF="#item_undef">undef</A> when they fail instead of causing your program to die right then and there
due to an uncaught exception. (Actually, some of the new <EM>Socket</EM> conversion functions <CODE>croak()</CODE> on bad arguments.) It is
therefore essential that you should check the return values of these
functions. Always begin your socket programs this way for optimal success,
and don't forget to add
<STRONG>-T</STRONG> taint checking flag to the pound-bang line for servers:


<P>

<PRE>    #!/usr/bin/perl -w
    require 5.002;
    use strict;
    use sigtrap;
    use Socket;
</PRE>

<P>

<P>
<HR>
<H1><A NAME="BUGS">BUGS

</A></H1>
All these routines create system-specific portability problems. As noted elsewhere, Perl is at the mercy of your 
<FONT SIZE=-1>C</FONT> libraries for much of its system behaviour. It's probably safest to assume broken SysV semantics for signals and to stick with simple 
<FONT SIZE=-1>TCP</FONT> and 
<FONT SIZE=-1>UDP</FONT> socket operations; e.g., don't try to pass open file descriptors over a local 
<FONT SIZE=-1>UDP</FONT> datagram socket if you want your code to stand a chance of being portable.



<P>

Because few vendors provide 
<FONT SIZE=-1>C</FONT> libraries that are safely re-entrant, the prudent
programmer will do little else within a handler beyond setting a numeric
variable that already exists; or, if locked into a slow (restarting) system
call, using <CODE>die()</CODE> to raise an exception and
<CODE>longjmp(3)</CODE> out. In fact, even these may in some cases cause a
core dump. It's probably best to avoid signals except where they are
absolutely inevitable. This perilous problems will be addressed in a future
release of Perl.


<P>

<P>
<HR>
<H1><A NAME="AUTHOR">AUTHOR

</A></H1>
Tom Christiansen, with occasional vestiges of Larry Wall's original version
and suggestions from the Perl Porters.


<P>

<P>
<HR>
<H1><A NAME="SEE_ALSO">SEE ALSO

</A></H1>
There's a lot more to networking than this, but this should get you
started.


<P>

For intrepid programmers, the classic textbook <EM>Unix Network Programming</EM>
by Richard Stevens (published by Addison-Wesley). Note that most books on networking address networking from the perspective of a 
<FONT SIZE=-1>C</FONT> programmer; translation to Perl is left as an exercise for the reader.



<P>

The IO::Socket(3) manpage describes the object library, and the
<CODE>Socket(3)</CODE> manpage describes the low-level interface to
sockets. Besides the obvious functions in <A HREF="/n|/perl/html/./lib/Pod/perlfunc.html#">the perlfunc manpage</A>, you should also check out the <EM>modules</EM> file at your nearest 
<FONT SIZE=-1>CPAN</FONT> site. (See <A HREF="/n|/perl/html/./lib/Pod/perlmodlib.html#">the perlmodlib manpage</A> or best yet, the <EM>Perl
FAQ</EM> for a description of what 
<FONT SIZE=-1>CPAN</FONT> is and where to get it.)


<P>

Section 5 of the <EM>modules</EM> file is devoted to ``Networking, Device Control (modems), and Interprocess Communication'', and contains numerous unbundled modules numerous networking modules, Chat and Expect operations, 
<FONT SIZE=-1>CGI</FONT> programming, 
<FONT SIZE=-1>DCE,</FONT> 
<FONT SIZE=-1>FTP,</FONT> 
<FONT SIZE=-1>IPC,</FONT> 
<FONT SIZE=-1>NNTP,</FONT> Proxy, Ptty, 
<FONT SIZE=-1>RPC,</FONT> 
<FONT SIZE=-1>SNMP,</FONT> 
<FONT SIZE=-1>SMTP,</FONT> Telnet, Threads, and ToolTalk--just to name a few.


<P>

</DL>
    </BODY>

    </HTML>

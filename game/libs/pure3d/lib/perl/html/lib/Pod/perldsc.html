    <HTML> 
	<HEAD> 
	    <TITLE>perldsc - Perl Data Structures Cookbook

</TITLE> 
	</HEAD>

	<BODY>

<!-- INDEX BEGIN -->

<UL>

	<LI><A HREF="#NAME">NAME</A>
	<LI><A HREF="#DESCRIPTION">DESCRIPTION</A>
	<LI><A HREF="#REFERENCES">REFERENCES</A>
	<LI><A HREF="#COMMON_MISTAKES">COMMON MISTAKES</A>
	<LI><A HREF="#CAVEAT_ON_PRECEDENCE">CAVEAT ON PRECEDENCE</A>
	<LI><A HREF="#WHY_YOU_SHOULD_ALWAYS_C_use_stri">WHY YOU SHOULD ALWAYS <CODE>use strict</CODE></A>
	<LI><A HREF="#DEBUGGING">DEBUGGING</A>
	<LI><A HREF="#CODE_EXAMPLES">CODE EXAMPLES</A>
	<LI><A HREF="#LISTS_OF_LISTS">LISTS OF LISTS</A>
	<UL>

		<LI><A HREF="#Declaration_of_a_LIST_OF_LISTS">Declaration of a LIST OF LISTS</A>
		<LI><A HREF="#Generation_of_a_LIST_OF_LISTS">Generation of a LIST OF LISTS</A>
		<LI><A HREF="#Access_and_Printing_of_a_LIST_OF">Access and Printing of a LIST OF LISTS</A>
	</UL>

	<LI><A HREF="#HASHES_OF_LISTS">HASHES OF LISTS</A>
	<UL>

		<LI><A HREF="#Declaration_of_a_HASH_OF_LISTS">Declaration of a HASH OF LISTS</A>
		<LI><A HREF="#Generation_of_a_HASH_OF_LISTS">Generation of a HASH OF LISTS</A>
		<LI><A HREF="#Access_and_Printing_of_a_HASH_OF">Access and Printing of a HASH OF LISTS</A>
	</UL>

	<LI><A HREF="#LISTS_OF_HASHES">LISTS OF HASHES</A>
	<UL>

		<LI><A HREF="#Declaration_of_a_LIST_OF_HASHES">Declaration of a LIST OF HASHES</A>
		<LI><A HREF="#Generation_of_a_LIST_OF_HASHES">Generation of a LIST OF HASHES</A>
		<LI><A HREF="#Access_and_Printing_of_a_LIST_OF">Access and Printing of a LIST OF HASHES</A>
	</UL>

	<LI><A HREF="#HASHES_OF_HASHES">HASHES OF HASHES</A>
	<UL>

		<LI><A HREF="#Declaration_of_a_HASH_OF_HASHES">Declaration of a HASH OF HASHES</A>
		<LI><A HREF="#Generation_of_a_HASH_OF_HASHES">Generation of a HASH OF HASHES</A>
		<LI><A HREF="#Access_and_Printing_of_a_HASH_OF">Access and Printing of a HASH OF HASHES</A>
	</UL>

	<LI><A HREF="#MORE_ELABORATE_RECORDS">MORE ELABORATE RECORDS</A>
	<UL>

		<LI><A HREF="#Declaration_of_MORE_ELABORATE_RE">Declaration of MORE ELABORATE RECORDS</A>
		<LI><A HREF="#Declaration_of_a_HASH_OF_COMPLEX">Declaration of a HASH OF COMPLEX RECORDS</A>
		<LI><A HREF="#Generation_of_a_HASH_OF_COMPLEX_">Generation of a HASH OF COMPLEX RECORDS</A>
	</UL>

	<LI><A HREF="#Database_Ties">Database Ties</A>
	<LI><A HREF="#SEE_ALSO">SEE ALSO</A>
	<LI><A HREF="#AUTHOR">AUTHOR</A>
</UL>
<!-- INDEX END -->

<HR>
<P>
<H1><A NAME="NAME">NAME

</A></H1>
perldsc - Perl Data Structures Cookbook


<P>

<P>
<HR>
<H1><A NAME="DESCRIPTION">DESCRIPTION

</A></H1>
The single feature most sorely lacking in the Perl programming language
prior to its 5.0 release was complex data structures. Even without direct
language support, some valiant programmers did manage to emulate them, but
it was hard work and not for the faint of heart. You could occasionally get
away with the <CODE>$m{$LoL,$b}</CODE> notation borrowed from <EM>awk</EM> in which the keys are actually more like a single concatenated string <CODE>"$LoL$b"</CODE>, but traversal and sorting were difficult. More desperate programmers even
hacked Perl's internal symbol table directly, a strategy that proved hard
to develop and maintain--to put it mildly.


<P>

The 5.0 release of Perl let us have complex data structures. You may now
write something like this and all of a sudden, you'd have a array with
three dimensions!


<P>

<PRE>    for $x (1 .. 10) {
        for $y (1 .. 10) {
            for $z (1 .. 10) {
                $LoL[$x][$y][$z] =
                    $x ** $y + $z;
            }
        }
    }
</PRE>

<P>

Alas, however simple this may appear, underneath it's a much more elaborate
construct than meets the eye!


<P>

How do you print it out? Why can't you say just <CODE>print @LoL</CODE>? How do you sort it? How can you pass it to a function or get one of these
back from a function? Is is an object? Can you save it to disk to read back
later? How do you access whole rows or columns of that matrix? Do all the
values have to be numeric?


<P>

As you see, it's quite easy to become confused. While some small portion of
the blame for this can be attributed to the reference-based implementation,
it's really more due to a lack of existing documentation with examples
designed for the beginner.


<P>

This document is meant to be a detailed but understandable treatment of the
many different sorts of data structures you might want to develop. It
should also serve as a cookbook of examples. That way, when you need to
create one of these complex data structures, you can just pinch, pilfer, or
purloin a drop-in example from here.


<P>

Let's look at each of these possible constructs in detail. There are
separate sections on each of the following:


<P>

<UL>
<LI><STRONG><A NAME="item_arrays">arrays of arrays

</A></STRONG>
<LI><STRONG><A NAME="item_hashes">hashes of arrays

</A></STRONG>
<LI><STRONG><A NAME="item_arrays">arrays of hashes

</A></STRONG>
<LI><STRONG><A NAME="item_hashes">hashes of hashes

</A></STRONG>
<LI><STRONG><A NAME="item_more">more elaborate constructs

</A></STRONG>
</UL>
But for now, let's look at some of the general issues common to all of
these types of data structures.


<P>

<P>
<HR>
<H1><A NAME="REFERENCES">REFERENCES

</A></H1>
The most important thing to understand about all data structures in Perl --
including multidimensional arrays--is that even though they might appear
otherwise, Perl <CODE>@ARRAY</CODE>s and <CODE>%HASH</CODE>es are all internally one-dimensional. They can hold only scalar values
(meaning a string, number, or a reference). They cannot directly contain
other arrays or hashes, but instead contain <EM>references</EM> to other arrays or hashes.


<P>

You can't use a reference to a array or hash in quite the same way that you would a real array or hash. For 
<FONT SIZE=-1>C</FONT> or 
<FONT SIZE=-1>C++</FONT> programmers unused to distinguishing between arrays and pointers to the same, this can be confusing. If so, just think of it as the difference between a structure and a pointer to a structure.



<P>

You can (and should) read more about references in the
<CODE>perlref(1)</CODE> man page. Briefly, references are rather like
pointers that know what they point to. (Objects are also a kind of
reference, but we won't be needing them right away--if ever.) This means
that when you have something which looks to you like an access to a
two-or-more-dimensional array and/or hash, what's really going on is that
the base type is merely a one-dimensional entity that contains references
to the next level. It's just that you can <EM>use</EM> it as though it were a two-dimensional one. This is actually the way almost all 
<FONT SIZE=-1>C</FONT> multidimensional arrays work as well.



<P>

<PRE>    $list[7][12]                        # array of arrays
    $list[7]{string}                    # array of hashes
    $hash{string}[7]                    # hash of arrays
    $hash{string}{'another string'}     # hash of hashes
</PRE>

<P>

Now, because the top level contains only references, if you try to print
out your array in with a simple <CODE>print()</CODE> function, you'll get
something that doesn't look very nice, like this:


<P>

<PRE>    @LoL = ( [2, 3], [4, 5, 7], [0] );
    print $LoL[1][2];
  7
    print @LoL;
  ARRAY(0x83c38)ARRAY(0x8b194)ARRAY(0x8b1d0)
</PRE>

<P>

That's because Perl doesn't (ever) implicitly dereference your variables.
If you want to get at the thing a reference is referring to, then you have
to do this yourself using either prefix typing indicators, like
<CODE>${$blah}</CODE>, <CODE>@{$blah}</CODE>, <CODE>@{$blah[$i]}</CODE>, or else postfix pointer arrows, like <CODE>$a-&gt;[3]</CODE>, <CODE>$h-&gt;{fred}</CODE>, or even <CODE>$ob-&gt;method()-&gt;[3]</CODE>.


<P>

<P>
<HR>
<H1><A NAME="COMMON_MISTAKES">COMMON MISTAKES

</A></H1>
The two most common mistakes made in constructing something like an array
of arrays is either accidentally counting the number of elements or else
taking a reference to the same memory location repeatedly. Here's the case
where you just get the count instead of a nested array:


<P>

<PRE>    for $i (1..10) {
        @list = somefunc($i);
        $LoL[$i] = @list;       # WRONG!
    }
</PRE>

<P>

That's just the simple case of assigning a list to a scalar and getting its
element count. If that's what you really and truly want, then you might do
well to consider being a tad more explicit about it, like this:


<P>

<PRE>    for $i (1..10) {
        @list = somefunc($i);
        $counts[$i] = scalar @list;
    }
</PRE>

<P>

Here's the case of taking a reference to the same memory location again and
again:


<P>

<PRE>    for $i (1..10) {
        @list = somefunc($i);
        $LoL[$i] = \@list;      # WRONG!
    }
</PRE>

<P>

So, what's the big problem with that? It looks right, doesn't it? After all, 
<FONT SIZE=-1>I</FONT> just told you that you need an array of references, so by golly, you've made me one!



<P>

Unfortunately, while this is true, it's still broken. All the references in
<CODE>@LoL</CODE> refer to the <EM>very same place</EM>, and they will therefore all hold whatever was last in @list! It's similar to the problem demonstrated in the following 
<FONT SIZE=-1>C</FONT> program:



<P>

<PRE>    #include &lt;pwd.h&gt;
    main() {
        struct passwd *getpwnam(), *rp, *dp;
        rp = getpwnam(&quot;root&quot;);
        dp = getpwnam(&quot;daemon&quot;);
</PRE>

<P>

<PRE>        printf(&quot;daemon name is %s\nroot name is %s\n&quot;,
                dp-&gt;pw_name, rp-&gt;pw_name);
    }
</PRE>

<P>

Which will print


<P>

<PRE>    daemon name is daemon
    root name is daemon
</PRE>

<P>

The problem is that both <CODE>rp</CODE> and <CODE>dp</CODE> are pointers to the same location in memory! In 
<FONT SIZE=-1>C,</FONT> you'd have to remember to <CODE>malloc()</CODE>
yourself some new memory. In Perl, you'll want to use the array constructor <CODE>[]</CODE> or the hash constructor <CODE>{}</CODE> instead. Here's the right way to do the preceding broken code fragments:


<P>

<PRE>    for $i (1..10) {
        @list = somefunc($i);
        $LoL[$i] = [ @list ];
    }
</PRE>

<P>

The square brackets make a reference to a new array with a <EM>copy</EM>
of what's in <CODE>@list</CODE> at the time of the assignment. This is what
you want.


<P>

Note that this will produce something similar, but it's much harder to
read:


<P>

<PRE>    for $i (1..10) {
        @list = 0 .. $i;
        @{$LoL[$i]} = @list;
    }
</PRE>

<P>

Is it the same? Well, maybe so--and maybe not. The subtle difference is
that when you assign something in square brackets, you know for sure it's
always a brand new reference with a new <EM>copy</EM> of the data. Something else could be going on in this new case with the <CODE>@{$LoL[$i]}}</CODE>
dereference on the left-hand-side of the assignment. It all depends on
whether <CODE>$LoL[$i]</CODE> had been undefined to start with, or whether it already contained a
reference. If you had already populated <CODE>@LoL</CODE> with references,
as in


<P>

<PRE>    $LoL[3] = \@another_list;
</PRE>

<P>

Then the assignment with the indirection on the left-hand-side would use
the existing reference that was already there:


<P>

<PRE>    @{$LoL[3]} = @list;
</PRE>

<P>

Of course, this <EM>would</EM> have the ``interesting'' effect of clobbering @another_list. (Have you ever
noticed how when a programmer says something is ``interesting'', that
rather than meaning ``intriguing'', they're disturbingly more apt to mean
that it's ``annoying'', ``difficult'', or both? :-)


<P>

So just remember always to use the array or hash constructors with <CODE>[]</CODE>
or <CODE>{}</CODE>, and you'll be fine, although it's not always optimally efficient.


<P>

Surprisingly, the following dangerous-looking construct will actually work
out fine:


<P>

<PRE>    for $i (1..10) {
        my @list = somefunc($i);
        $LoL[$i] = \@list;
    }
</PRE>

<P>

That's because <CODE>my()</CODE> is more of a run-time statement than it is
a compile-time declaration <EM>per se</EM>. This means that the <CODE>my()</CODE> variable is remade afresh each time
through the loop. So even though it <EM>looks</EM> as though you stored the same variable reference each time, you actually did not! This is a subtle distinction that can produce more efficient code at the risk of misleading all but the most experienced of programmers. So 
<FONT SIZE=-1>I</FONT> usually advise against teaching it to beginners. In fact, except for passing arguments to functions, 
<FONT SIZE=-1>I</FONT> seldom like to see the gimme-a-reference operator (backslash) used much at all in code. Instead, 
<FONT SIZE=-1>I</FONT> advise beginners that they (and most of the rest of us) should try to use the much more easily understood constructors
 <CODE>[]</CODE> and <CODE>{}</CODE> instead of relying upon lexical (or dynamic) scoping and hidden
reference-counting to do the right thing behind the scenes.


<P>

In summary:


<P>

<PRE>    $LoL[$i] = [ @list ];       # usually best
    $LoL[$i] = \@list;          # perilous; just how my() was that list?
    @{ $LoL[$i] } = @list;      # way too tricky for most programmers
</PRE>

<P>

<P>
<HR>
<H1><A NAME="CAVEAT_ON_PRECEDENCE">CAVEAT ON PRECEDENCE

</A></H1>
Speaking of things like <CODE>@{$LoL[$i]}</CODE>, the following are actually the same thing:


<P>

<PRE>    $listref-&gt;[2][2]    # clear
    $$listref[2][2]     # confusing
</PRE>

<P>

That's because Perl's precedence rules on its five prefix dereferencers
(which look like someone swearing: <CODE>$ @ * % &</CODE>) make them bind more tightly than the postfix subscripting brackets or braces! This will no doubt come as a great shock to the 
<FONT SIZE=-1>C</FONT> or 
<FONT SIZE=-1>C++</FONT> programmer, who is quite accustomed to using
 <CODE>*a[i]</CODE> to mean what's pointed to by the <EM>i'th</EM>
element of <A HREF="#item_a">a</A>. That is, they first take the subscript, and only then dereference the thing at that subscript. That's fine in 
<FONT SIZE=-1>C,</FONT> but this isn't 
<FONT SIZE=-1>C.</FONT>



<P>

The seemingly equivalent construct in Perl, <CODE>$$listref[$i]</CODE> first does the deref of <CODE>$listref</CODE>, making it take <CODE>$listref</CODE> as a reference to an array, and then
dereference that, and finally tell you the <EM>i'th</EM> value of the array pointed to by $LoL. If you wanted the 
<FONT SIZE=-1>C</FONT> notion, you'd have to write <CODE>${$LoL[$i]}</CODE> to force the <CODE>$LoL[$i]</CODE> to get evaluated first before the leading <CODE>$</CODE> dereferencer.


<P>

<P>
<HR>
<H1><A NAME="WHY_YOU_SHOULD_ALWAYS_C_use_stri">WHY YOU SHOULD ALWAYS <CODE>use strict</CODE>

</A></H1>
If this is starting to sound scarier than it's worth, relax. Perl has some
features to help you avoid its most common pitfalls. The best way to avoid
getting confused is to start every program like this:


<P>

<PRE>    #!/usr/bin/perl -w
    use strict;
</PRE>

<P>

This way, you'll be forced to declare all your variables with
<CODE>my()</CODE> and also disallow accidental ``symbolic dereferencing''.
Therefore if you'd done this:


<P>

<PRE>    my $listref = [
        [ &quot;fred&quot;, &quot;barney&quot;, &quot;pebbles&quot;, &quot;bambam&quot;, &quot;dino&quot;, ],
        [ &quot;homer&quot;, &quot;bart&quot;, &quot;marge&quot;, &quot;maggie&quot;, ],
        [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot;, &quot;judy&quot;, ],
    ];
</PRE>

<P>

<PRE>    print $listref[2][2];
</PRE>

<P>

The compiler would immediately flag that as an error <EM>at compile time</EM>, because you were accidentally accessing <CODE>@listref</CODE>, an undeclared variable, and it would thereby remind you to write instead:


<P>

<PRE>    print $listref-&gt;[2][2]
</PRE>

<P>

<P>
<HR>
<H1><A NAME="DEBUGGING">DEBUGGING

</A></H1>
Before version 5.002, the standard Perl debugger didn't do a very nice job
of printing out complex data structures. With 5.002 or above, the debugger
includes several new features, including command line editing as well as
the <A HREF="#item_x">x</A> command to dump out complex data structures. For example, given the
assignment to <CODE>$LoL</CODE> above, here's the debugger output:


<P>

<PRE>    DB&lt;1&gt; X $LoL
    $LoL = ARRAY(0x13b5a0)
       0  ARRAY(0x1f0a24)
          0  'fred'
          1  'barney'
          2  'pebbles'
          3  'bambam'
          4  'dino'
       1  ARRAY(0x13b558)
          0  'homer'
          1  'bart'
          2  'marge'
          3  'maggie'
       2  ARRAY(0x13b540)
          0  'george'
          1  'jane'
          2  'elroy'
          3  'judy'
</PRE>

<P>

There's also a lowercase <STRONG>x</STRONG> command which is nearly the same.


<P>

<P>
<HR>
<H1><A NAME="CODE_EXAMPLES">CODE EXAMPLES

</A></H1>
Presented with little comment (these will get their own manpages someday)
here are short code examples illustrating access of various types of data
structures.


<P>

<P>
<HR>
<H1><A NAME="LISTS_OF_LISTS">LISTS OF LISTS

</A></H1>
<P>
<HR>
<H2><A NAME="Declaration_of_a_LIST_OF_LISTS">Declaration of a LIST OF LISTS

</A></H2>
<PRE> @LoL = (
        [ &quot;fred&quot;, &quot;barney&quot; ],
        [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
        [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
      );
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Generation_of_a_LIST_OF_LISTS">Generation of a LIST OF LISTS

</A></H2>
<PRE> # reading from file
 while ( &lt;&gt; ) {
     push @LoL, [ split ];
 }
</PRE>

<P>

<PRE> # calling a function
 for $i ( 1 .. 10 ) {
     $LoL[$i] = [ somefunc($i) ];
 }
</PRE>

<P>

<PRE> # using temp vars
 for $i ( 1 .. 10 ) {
     @tmp = somefunc($i);
     $LoL[$i] = [ @tmp ];
 }
</PRE>

<P>

<PRE> # add to an existing row
 push @{ $LoL[0] }, &quot;wilma&quot;, &quot;betty&quot;;
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Access_and_Printing_of_a_LIST_OF">Access and Printing of a LIST OF LISTS

</A></H2>
<PRE> # one element
 $LoL[0][0] = &quot;Fred&quot;;
</PRE>

<P>

<PRE> # another element
 $LoL[1][1] =~ s/(\w)/\u$1/;
</PRE>

<P>

<PRE> # print the whole thing with refs
 for $aref ( @LoL ) {
     print &quot;\t [ @$aref ],\n&quot;;
 }
</PRE>

<P>

<PRE> # print the whole thing with indices
 for $i ( 0 .. $#LoL ) {
     print &quot;\t [ @{$LoL[$i]} ],\n&quot;;
 }
</PRE>

<P>

<PRE> # print the whole thing one at a time
 for $i ( 0 .. $#LoL ) {
     for $j ( 0 .. $#{ $LoL[$i] } ) {
         print &quot;elt $i $j is $LoL[$i][$j]\n&quot;;
     }
 }
</PRE>

<P>

<P>
<HR>
<H1><A NAME="HASHES_OF_LISTS">HASHES OF LISTS

</A></H1>
<P>
<HR>
<H2><A NAME="Declaration_of_a_HASH_OF_LISTS">Declaration of a HASH OF LISTS

</A></H2>
<PRE> %HoL = (
        flintstones        =&gt; [ &quot;fred&quot;, &quot;barney&quot; ],
        jetsons            =&gt; [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
        simpsons           =&gt; [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
      );
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Generation_of_a_HASH_OF_LISTS">Generation of a HASH OF LISTS

</A></H2>
<PRE> # reading from file
 # flintstones: fred barney wilma dino
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $HoL{$1} = [ split ];
 }
</PRE>

<P>

<PRE> # reading from file; more temps
 # flintstones: fred barney wilma dino
 while ( $line = &lt;&gt; ) {
     ($who, $rest) = split /:\s*/, $line, 2;
     @fields = split ' ', $rest;
     $HoL{$who} = [ @fields ];
 }
</PRE>

<P>

<PRE> # calling a function that returns a list
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     $HoL{$group} = [ get_family($group) ];
 }
</PRE>

<P>

<PRE> # likewise, but using temps
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     @members = get_family($group);
     $HoL{$group} = [ @members ];
 }
</PRE>

<P>

<PRE> # append new members to an existing family
 push @{ $HoL{&quot;flintstones&quot;} }, &quot;wilma&quot;, &quot;betty&quot;;
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Access_and_Printing_of_a_HASH_OF">Access and Printing of a HASH OF LISTS

</A></H2>
<PRE> # one element
 $HoL{flintstones}[0] = &quot;Fred&quot;;
</PRE>

<P>

<PRE> # another element
 $HoL{simpsons}[1] =~ s/(\w)/\u$1/;
</PRE>

<P>

<PRE> # print the whole thing
 foreach $family ( keys %HoL ) {
     print &quot;$family: @{ $HoL{$family} }\n&quot;
 }
</PRE>

<P>

<PRE> # print the whole thing with indices
 foreach $family ( keys %HoL ) {
     print &quot;family: &quot;;
     foreach $i ( 0 .. $#{ $HoL{$family} } ) {
         print &quot; $i = $HoL{$family}[$i]&quot;;
     }
     print &quot;\n&quot;;
 }
</PRE>

<P>

<PRE> # print the whole thing sorted by number of members
 foreach $family ( sort { @{$HoL{$b}} &lt;=&gt; @{$HoL{$a}} } keys %HoL ) {
     print &quot;$family: @{ $HoL{$family} }\n&quot;
 }
</PRE>

<P>

<PRE> # print the whole thing sorted by number of members and name
 foreach $family ( sort {
                            @{$HoL{$b}} &lt;=&gt; @{$HoL{$a}}
                                        ||
                                    $a cmp $b
            } keys %HoL )
 {
     print &quot;$family: &quot;, join(&quot;, &quot;, sort @{ $HoL{$family}), &quot;\n&quot;;
 }
</PRE>

<P>

<P>
<HR>
<H1><A NAME="LISTS_OF_HASHES">LISTS OF HASHES

</A></H1>
<P>
<HR>
<H2><A NAME="Declaration_of_a_LIST_OF_HASHES">Declaration of a LIST OF HASHES

</A></H2>
<PRE> @LoH = (
        {
            Lead     =&gt; &quot;fred&quot;,
            Friend   =&gt; &quot;barney&quot;,
        },
        {
            Lead     =&gt; &quot;george&quot;,
            Wife     =&gt; &quot;jane&quot;,
            Son      =&gt; &quot;elroy&quot;,
        },
        {
            Lead     =&gt; &quot;homer&quot;,
            Wife     =&gt; &quot;marge&quot;,
            Son      =&gt; &quot;bart&quot;,
        }
  );
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Generation_of_a_LIST_OF_HASHES">Generation of a LIST OF HASHES

</A></H2>
<PRE> # reading from file
 # format: LEAD=fred FRIEND=barney
 while ( &lt;&gt; ) {
     $rec = {};
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $rec-&gt;{$key} = $value;
     }
     push @LoH, $rec;
 }
</PRE>

<P>

<PRE> # reading from file
 # format: LEAD=fred FRIEND=barney
 # no temp
 while ( &lt;&gt; ) {
     push @LoH, { split /[\s+=]/ };
 }
</PRE>

<P>

<PRE> # calling a function  that returns a key,value list, like
 # &quot;lead&quot;,&quot;fred&quot;,&quot;daughter&quot;,&quot;pebbles&quot;
 while ( %fields = getnextpairset() ) {
     push @LoH, { %fields };
 }
</PRE>

<P>

<PRE> # likewise, but using no temp vars
 while (&lt;&gt;) {
     push @LoH, { parsepairs($_) };
 }
</PRE>

<P>

<PRE> # add key/value to an element
 $LoH[0]{pet} = &quot;dino&quot;;
 $LoH[2]{pet} = &quot;santa's little helper&quot;;
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Access_and_Printing_of_a_LIST_OF">Access and Printing of a LIST OF HASHES

</A></H2>
<PRE> # one element
 $LoH[0]{lead} = &quot;fred&quot;;
</PRE>

<P>

<PRE> # another element
 $LoH[1]{lead} =~ s/(\w)/\u$1/;
</PRE>

<P>

<PRE> # print the whole thing with refs
 for $href ( @LoH ) {
     print &quot;{ &quot;;
     for $role ( keys %$href ) {
         print &quot;$role=$href-&gt;{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }
</PRE>

<P>

<PRE> # print the whole thing with indices
 for $i ( 0 .. $#LoH ) {
     print &quot;$i is { &quot;;
     for $role ( keys %{ $LoH[$i] } ) {
         print &quot;$role=$LoH[$i]{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }
</PRE>

<P>

<PRE> # print the whole thing one at a time
 for $i ( 0 .. $#LoH ) {
     for $role ( keys %{ $LoH[$i] } ) {
         print &quot;elt $i $role is $LoH[$i]{$role}\n&quot;;
     }
 }
</PRE>

<P>

<P>
<HR>
<H1><A NAME="HASHES_OF_HASHES">HASHES OF HASHES

</A></H1>
<P>
<HR>
<H2><A NAME="Declaration_of_a_HASH_OF_HASHES">Declaration of a HASH OF HASHES

</A></H2>
<PRE> %HoH = (
        flintstones =&gt; {
                lead      =&gt; &quot;fred&quot;,
                pal       =&gt; &quot;barney&quot;,
        },
        jetsons     =&gt; {
                lead      =&gt; &quot;george&quot;,
                wife      =&gt; &quot;jane&quot;,
                &quot;his boy&quot; =&gt; &quot;elroy&quot;,
        },
        simpsons    =&gt; {
                lead      =&gt; &quot;homer&quot;,
                wife      =&gt; &quot;marge&quot;,
                kid       =&gt; &quot;bart&quot;,
        },
 );
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Generation_of_a_HASH_OF_HASHES">Generation of a HASH OF HASHES

</A></H2>
<PRE> # reading from file
 # flintstones: lead=fred pal=barney wife=wilma pet=dino
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $who = $1;
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $HoH{$who}{$key} = $value;
     }
</PRE>

<P>

<PRE> # reading from file; more temps
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $who = $1;
     $rec = {};
     $HoH{$who} = $rec;
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $rec-&gt;{$key} = $value;
     }
 }
</PRE>

<P>

<PRE> # calling a function  that returns a key,value hash
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     $HoH{$group} = { get_family($group) };
 }
</PRE>

<P>

<PRE> # likewise, but using temps
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     %members = get_family($group);
     $HoH{$group} = { %members };
 }
</PRE>

<P>

<PRE> # append new members to an existing family
 %new_folks = (
     wife =&gt; &quot;wilma&quot;,
     pet  =&gt; &quot;dino&quot;;
 );
</PRE>

<P>

<PRE> for $what (keys %new_folks) {
     $HoH{flintstones}{$what} = $new_folks{$what};
 }
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Access_and_Printing_of_a_HASH_OF">Access and Printing of a HASH OF HASHES

</A></H2>
<PRE> # one element
 $HoH{flintstones}{wife} = &quot;wilma&quot;;
</PRE>

<P>

<PRE> # another element
 $HoH{simpsons}{lead} =~ s/(\w)/\u$1/;
</PRE>

<P>

<PRE> # print the whole thing
 foreach $family ( keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }
</PRE>

<P>

<PRE> # print the whole thing  somewhat sorted
 foreach $family ( sort keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( sort keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }
</PRE>

<P>

<PRE> # print the whole thing sorted by number of members
 foreach $family ( sort { keys %{$HoH{$b}} &lt;=&gt; keys %{$HoH{$a}} } keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( sort keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }
</PRE>

<P>

<PRE> # establish a sort order (rank) for each role
 $i = 0;
 for ( qw(lead wife son daughter pal pet) ) { $rank{$_} = ++$i }
</PRE>

<P>

<PRE> # now print the whole thing sorted by number of members
 foreach $family ( sort { keys %{ $HoH{$b} } &lt;=&gt; keys %{ $HoH{$a} } } keys %HoH ) {
     print &quot;$family: { &quot;;
     # and print these according to rank order
     for $role ( sort { $rank{$a} &lt;=&gt; $rank{$b} }  keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }
</PRE>

<P>

<P>
<HR>
<H1><A NAME="MORE_ELABORATE_RECORDS">MORE ELABORATE RECORDS

</A></H1>
<P>
<HR>
<H2><A NAME="Declaration_of_MORE_ELABORATE_RE">Declaration of MORE ELABORATE RECORDS

</A></H2>
Here's a sample showing how to create and use a record whose fields are of
many different sorts:


<P>

<PRE>     $rec = {
         TEXT      =&gt; $string,
         SEQUENCE  =&gt; [ @old_values ],
         LOOKUP    =&gt; { %some_table },
         THATCODE  =&gt; \&amp;some_function,
         THISCODE  =&gt; sub { $_[0] ** $_[1] },
         HANDLE    =&gt; \*STDOUT,
     };
</PRE>

<P>

<PRE>     print $rec-&gt;{TEXT};
</PRE>

<P>

<PRE>     print $rec-&gt;{LIST}[0];
     $last = pop @ { $rec-&gt;{SEQUENCE} };
</PRE>

<P>

<PRE>     print $rec-&gt;{LOOKUP}{&quot;key&quot;};
     ($first_k, $first_v) = each %{ $rec-&gt;{LOOKUP} };
</PRE>

<P>

<PRE>     $answer = $rec-&gt;{THATCODE}-&gt;($arg);
     $answer = $rec-&gt;{THISCODE}-&gt;($arg1, $arg2);
</PRE>

<P>

<PRE>     # careful of extra block braces on fh ref
     print { $rec-&gt;{HANDLE} } &quot;a string\n&quot;;
</PRE>

<P>

<PRE>     use FileHandle;
     $rec-&gt;{HANDLE}-&gt;autoflush(1);
     $rec-&gt;{HANDLE}-&gt;print(&quot; a string\n&quot;);
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Declaration_of_a_HASH_OF_COMPLEX">Declaration of a HASH OF COMPLEX RECORDS

</A></H2>
<PRE>     %TV = (
        flintstones =&gt; {
            series   =&gt; &quot;flintstones&quot;,
            nights   =&gt; [ qw(monday thursday friday) ],
            members  =&gt; [
                { name =&gt; &quot;fred&quot;,    role =&gt; &quot;lead&quot;, age  =&gt; 36, },
                { name =&gt; &quot;wilma&quot;,   role =&gt; &quot;wife&quot;, age  =&gt; 31, },
                { name =&gt; &quot;pebbles&quot;, role =&gt; &quot;kid&quot;,  age  =&gt;  4, },
            ],
        },
</PRE>

<P>

<PRE>        jetsons     =&gt; {
            series   =&gt; &quot;jetsons&quot;,
            nights   =&gt; [ qw(wednesday saturday) ],
            members  =&gt; [
                { name =&gt; &quot;george&quot;,  role =&gt; &quot;lead&quot;, age  =&gt; 41, },
                { name =&gt; &quot;jane&quot;,    role =&gt; &quot;wife&quot;, age  =&gt; 39, },
                { name =&gt; &quot;elroy&quot;,   role =&gt; &quot;kid&quot;,  age  =&gt;  9, },
            ],
         },
</PRE>

<P>

<PRE>        simpsons    =&gt; {
            series   =&gt; &quot;simpsons&quot;,
            nights   =&gt; [ qw(monday) ],
            members  =&gt; [
                { name =&gt; &quot;homer&quot;, role =&gt; &quot;lead&quot;, age  =&gt; 34, },
                { name =&gt; &quot;marge&quot;, role =&gt; &quot;wife&quot;, age =&gt; 37, },
                { name =&gt; &quot;bart&quot;,  role =&gt; &quot;kid&quot;,  age  =&gt;  11, },
            ],
         },
      );
</PRE>

<P>

<P>
<HR>
<H2><A NAME="Generation_of_a_HASH_OF_COMPLEX_">Generation of a HASH OF COMPLEX RECORDS

</A></H2>
<PRE>     # reading from file
     # this is most easily done by having the file itself be
     # in the raw data format as shown above.  perl is happy
     # to parse complex data structures if declared as data, so
     # sometimes it's easiest to do that
</PRE>

<P>

<PRE>     # here's a piece by piece build up
     $rec = {};
     $rec-&gt;{series} = &quot;flintstones&quot;;
     $rec-&gt;{nights} = [ find_days() ];
</PRE>

<P>

<PRE>     @members = ();
     # assume this file in field=value syntax
     while (&lt;&gt;) {
         %fields = split /[\s=]+/;
         push @members, { %fields };
     }
     $rec-&gt;{members} = [ @members ];
</PRE>

<P>

<PRE>     # now remember the whole thing
     $TV{ $rec-&gt;{series} } = $rec;
</PRE>

<P>

<PRE>     ###########################################################
     # now, you might want to make interesting extra fields that
     # include pointers back into the same data structure so if
     # change one piece, it changes everywhere, like for examples
     # if you wanted a {kids} field that was an array reference
     # to a list of the kids' records without having duplicate
     # records and thus update problems.
     ###########################################################
     foreach $family (keys %TV) {
         $rec = $TV{$family}; # temp pointer
         @kids = ();
         for $person ( @{ $rec-&gt;{members} } ) {
             if ($person-&gt;{role} =~ /kid|son|daughter/) {
                 push @kids, $person;
             }
         }
         # REMEMBER: $rec and $TV{$family} point to same data!!
         $rec-&gt;{kids} = [ @kids ];
     }
</PRE>

<P>

<PRE>     # you copied the list, but the list itself contains pointers
     # to uncopied objects. this means that if you make bart get
     # older via
</PRE>

<P>

<PRE>     $TV{simpsons}{kids}[0]{age}++;
</PRE>

<P>

<PRE>     # then this would also change in
     print $TV{simpsons}{members}[2]{age};
</PRE>

<P>

<PRE>     # because $TV{simpsons}{kids}[0] and $TV{simpsons}{members}[2]
     # both point to the same underlying anonymous hash table
</PRE>

<P>

<PRE>     # print the whole thing
     foreach $family ( keys %TV ) {
         print &quot;the $family&quot;;
         print &quot; is on during @{ $TV{$family}{nights} }\n&quot;;
         print &quot;its members are:\n&quot;;
         for $who ( @{ $TV{$family}{members} } ) {
             print &quot; $who-&gt;{name} ($who-&gt;{role}), age $who-&gt;{age}\n&quot;;
         }
         print &quot;it turns out that $TV{$family}{lead} has &quot;;
         print scalar ( @{ $TV{$family}{kids} } ), &quot; kids named &quot;;
         print join (&quot;, &quot;, map { $_-&gt;{name} } @{ $TV{$family}{kids} } );
         print &quot;\n&quot;;
     }
</PRE>

<P>

<P>
<HR>
<H1><A NAME="Database_Ties">Database Ties

</A></H1>
You cannot easily tie a multilevel data structure (such as a hash of hashes) to a dbm file. The first problem is that all but 
<FONT SIZE=-1>GDBM</FONT> and Berkeley 
<FONT SIZE=-1>DB</FONT> have size limitations, but beyond that, you also have problems with how references are to be represented on disk. One experimental module that does partially attempt to address this need is the 
<FONT SIZE=-1>MLDBM</FONT> module. Check your nearest 
<FONT SIZE=-1>CPAN</FONT> site as described in
 <A HREF="/n|/perl/html/./lib/Pod/perlmodlib.html#">the perlmodlib manpage</A> for source code to 
<FONT SIZE=-1>MLDBM.</FONT>


<P>

<P>
<HR>
<H1><A NAME="SEE_ALSO">SEE ALSO

</A></H1>
<CODE>perlref(1),</CODE> <CODE>perllol(1),</CODE> <CODE>perldata(1),</CODE>
<CODE>perlobj(1)</CODE>


<P>

<P>
<HR>
<H1><A NAME="AUTHOR">AUTHOR

</A></H1>
Tom Christiansen &lt;<EM>tchrist@perl.com</EM>&gt;


<P>

Last update: Wed Oct 23 04:57:50 
<FONT SIZE=-1>MET</FONT> 
<FONT SIZE=-1>DST</FONT> 1996


<P>

</DL>
    </BODY>

    </HTML>

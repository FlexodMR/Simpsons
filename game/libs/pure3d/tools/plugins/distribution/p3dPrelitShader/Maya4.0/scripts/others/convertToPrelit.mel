global proc convertToPrelit()
{
   // convert the simple shaders
   string $simplematerials[] = `ls -type pure3dSimpleShader`;

   for ($material in $simplematerials)
   { 
      rename $material p3dTempConversionMaterial;
      createNode pure3dPrelitShader -name $material;
      replaceNode("p3dTempConversionMaterial", $material); 
      // turn off the lighting if it was on in the simepl shader
      setAttr ($material+".exportLIT") 0;
      delete p3dTempConversionMaterial;
   }

   // Now the lamberts
   string $lambertmaterials[] = `ls -type lambert`;
   for ($material in $lambertmaterials)
   { 
      if (strcmp($material,"lambert1")==0)
      {
         continue;
      }      

      rename $material p3dTempConversionMaterial;
      createNode pure3dPrelitShader -name $material;

      //copy the attribute values
      float $colour[3] = `getAttr p3dTempConversionMaterial.color`;
      setAttr -type float3 ($material+".color") $colour[0] $colour[1] $colour[2];
      setAttr ($material+".blendMode") `getAttr p3dTempConversionMaterial.p3dBlendMode`;

      string $sourceConnections[] = `listConnections -connections true -plugs true -source true -destination false p3dTempConversionMaterial`;
      string $destConnections[] = `listConnections -connections true -plugs true -source false -destination true p3dTempConversionMaterial`;
      delete p3dTempConversionMaterial;

      int $current;

      for ($current = 0; $current < size($sourceConnections); $current+=2)
      {
         string $buffer[];
         tokenize $sourceConnections[$current] "." $buffer;
         string $attr = $buffer[1];

         if (`attributeQuery -exists -type pure3dPrelitShader $attr`==1)
         {
            if (`isConnected $sourceConnections[$current+1] ($material+"."+$attr)`==0)
            {
               connectAttr $sourceConnections[$current+1] ($material+"."+$attr); 
            }
         }
      }

      for ($current = 0; $current < size($destConnections); $current+=2)
      {
         string $buffer[];
         tokenize $destConnections[$current] "." $buffer;
         string $attr = $buffer[1];

         if (`attributeQuery -exists -type pure3dPrelitShader $attr`==1)
         {
            if (`isConnected ($material+"."+$attr) $destConnections[$current+1]`==0)
            {
               connectAttr ($material+"."+$attr) $destConnections[$current+1]; 
            }
         }
      }
   }
}
